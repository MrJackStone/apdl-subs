! BATCH_SLICEYZ, xPosArray, relative, sepo, mode
! BATCH_SLICEYZ,      arg1,     arg2, arg3, arg4

! Description:
! Performs many YZ plane area slicing operations at given X locations.

! Arguments
! arg1: xPosArray  -  name of the array parameter (string) containing slicing X coordinates
! arg2: relative   -  controls relative or absolute location behavior:
!                        [0] ABSOLUTE: interpret X as global cartesian coordinates
!                        [1] RELATIVE: interpret X as increments from current working plane location
! arg3: sepo       -  create merged (0) or separated (1) entities at slicing interfaces
! arg4: mode       -  [0] Areas; [1] Lines

! Local parameters:
! AR20: sliceCount
! AR21: nthSlice
! AR22: xCoord


*get,AR20,parm,%arg1%,dim,x

*do,AR21,1,AR20,1
    AR22=%arg1%(AR21)

    *if,arg2,eq,1,then
        ! Move working plane from its current position
        wpoffs,AR22
        wprota,,,90
        *if,arg3,eq,1,then
            *if,arg4,eq,0,then
                asbw,all,sepo,delete
            *elseif,arg4,eq,1,then
                lsbw,all,sepo,delete
            *endif
        *else
            *if,arg4,eq,0,then
                asbw,all,,delete
            *elseif,arg4,eq,1,then
                lsbw,all,,delete
            *endif
        *endif
        wprota,,,-90
        wpoffs,-AR22

    *else
        ! Position working plane using global cartesian coordinates
        wpsliceyz,AR22,arg3,arg4

    *endif
*enddo




