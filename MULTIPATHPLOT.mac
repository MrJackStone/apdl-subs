! MULTIPATHPLOT, imgName, baseScale,   x0,   y0
! MULTIPATHPLOT,    arg1,      arg2, arg3, arg4

! Arguments:
! arg1: imgName    -  name given to the captured image
! arg2: baseScale  -  scale factor applied to plotted path results
! arg3: x0         -  X screen coordinate of displayed texts
! arg4: y0         -  Y screen coordinate of the first text line displayed

! Local parameters:
! AR20: deltaYLoc
! AR21: pathCount
! AR22: nthPath
! AR23: pathName
! AR24: pathLabel
! AR25: minVal
! AR26: maxVal
! AR27: absmax
! AR28: minScale
! AR29: maxScale
! AR30: absPeak
! AR31: nthAbs
! AR32: xloc
! AR33: yloc
! AR34: pathScale
! AR35: partName
! AR38: plotScale
! AR39: parType

! Implicit arguments:
! MULTIPATHPLOT_ARR_pathNames  -  names of the paths to be plotted
! MULTIPATHPLOT_ARR_labels     -  label to be plotted for each path
! MULTIPATHPLOT_ARR_parts      -  part name (displayed as text on screen)

! Global parameters (erased at the end of this subroutine):
! MULTIPATHPLOT_ARR_min
! MULTIPATHPLOT_ARR_max
! MULTIPATHPLOT_ARR_absmax
! MULTIPATHPLOT_ARR_scales

! Distance between text lines
AR20=-0.05

*get,AR39,PARM,arg2,TYPE
*if,AR39,NE,0,OR,arg2,LE,0,THEN
    AR38=1
*else
    AR38=arg2
*endif

! Get number of paths to be plotted
*get,AR21,PARM,MULTIPATHPLOT_ARR_pathNames,DIM,X

*del,MULTIPATHPLOT_ARR_min
*del,MULTIPATHPLOT_ARR_max
*del,MULTIPATHPLOT_ARR_absmax
*del,MULTIPATHPLOT_ARR_scales
*dim,MULTIPATHPLOT_ARR_min,ARRAY,AR21
*dim,MULTIPATHPLOT_ARR_max,ARRAY,AR21
*dim,MULTIPATHPLOT_ARR_absmax,ARRAY,AR21
*dim,MULTIPATHPLOT_ARR_scales,ARRAY,AR21

! Get value envelopes for each path
*do,AR22,1,AR21,1
    AR23=MULTIPATHPLOT_ARR_pathNames(AR22)
    AR24=MULTIPATHPLOT_ARR_labels(AR22)
    PATH,AR23
    PTHLIMS,AR24
    AR25=PTHLIMS_MIN_OUTPUT
    AR26=PTHLIMS_MAX_OUTPUT
    AR27=PTHLIMS_ABS_OUTPUT
    MULTIPATHPLOT_ARR_min(AR22)=AR25
    MULTIPATHPLOT_ARR_max(AR22)=AR26
    MULTIPATHPLOT_ARR_absmax(AR22)=AR27
*enddo

! Calculate overall extremes
*vscfun,AR28,MIN,MULTIPATHPLOT_ARR_min
*vscfun,AR29,MAX,MULTIPATHPLOT_ARR_max
*vscfun,AR30,MAX,MULTIPATHPLOT_ARR_absmax
CLIMS,AR28,AR29

! Calculate plot scales
*do,AR22,1,AR21,1
    AR31=MULTIPATHPLOT_ARR_absmax(AR22)
    MULTIPATHPLOT_ARR_scales(AR22)=AR38*AR31/AR30
*enddo

! Output display to file
GO2PNG
/device,VECTOR,ON
EPLOT
/device,VECTOR,OFF
/noerase

! Plot texts
AR32=arg3
AR33=arg4
*cfopen,tmpinp,inp
    *do,AR22,1,AR21,1
        AR35=MULTIPATHPLOT_ARR_parts(AR22)
        AR25=MULTIPATHPLOT_ARR_min(AR22)
        AR26=MULTIPATHPLOT_ARR_max(AR22)

        *vwrite,AR32,AR33,AR35,AR25,AR26
        ('/tlabel,',F30.10,',',F30.10,',',A8,': ',F15.2,' ; ',F15.2)

        AR33=AR33+AR20
    *enddo
*cfclos
/nopr
/input,tmpinp,inp
/delete,tmpinp,inp
/gopr

! Plot paths
*do,AR22,1,AR21,1
    AR23=MULTIPATHPLOT_ARR_pathNames(AR22)
    AR24=MULTIPATHPLOT_ARR_labels(AR22)
    AR34=MULTIPATHPLOT_ARR_scales(AR22)
    AR35=MULTIPATHPLOT_ARR_parts(AR22)
    AR25=MULTIPATHPLOT_ARR_min(AR22)
    AR26=MULTIPATHPLOT_ARR_max(AR22)

    PATH,AR23
    PLPAGM,AR24,AR34,NODE
*enddo

! Finish image capture
ENDPNG,arg1
/erase
/annot,DELE
CLIMS

! Clear parameters
*del,MULTIPATHPLOT_ARR_min
*del,MULTIPATHPLOT_ARR_max
*del,MULTIPATHPLOT_ARR_absmax
*del,MULTIPATHPLOT_ARR_scales


