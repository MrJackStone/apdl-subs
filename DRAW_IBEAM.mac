! DRAW_IBEAM,    L,   bf,   dm
! DRAW_IBEAM, arg1, arg2, arg3

! Description:
! Draws the six areas of a doubly-symmetric I beam in the active coordinate system (web and each
! flange split into two areas). The origin of the active CSYS is placed at the intersection between
! the bottom flange and the web, at the beginning of the beam. Length is drawn along X, depth along
! Y, and width along Z.

! Arguments:
! arg1: L   -  span length (X)
! arg2: bf  -  flange width (Z)
! arg3: dm  -  section depth (Y)

! Local parameters:
! AR21: nthLine
! AR22: lnum
! AR28: COMPK_NEW
! AR29: COMPL_NEW
! AR30: COMPA_NEW
! AR31: lineCount
! AR32: axisLine

! Global parameters (erased at the end of this subroutine):
! DRAW_IBEAM_ARR_LNUMS
! DRAW_IBEAM_ARR_KPNUMS

! Output parameters:
! DRAW_IBEAM_COMPA_WEB
!    Area selection component containing the beam's web
! DRAW_IBEAM_COMPA_FLANGES
!    Area selection component containing the beam's flanges


/prep7
seltol,1e-8
/uis,MSGPOP,3

kbackup
lbackup
abackup

ksel,u,kp,,all
lsel,u,line,,all
asel,u,area,,all

! Calculate number of new keypoints
*get,AR31,KP,0,NUM,MAXD
*dim,DRAW_IBEAM_ARR_KPNUMS,ARRAY,8
*vfill,DRAW_IBEAM_ARR_KPNUMS,RAMP,AR31+1,1
numstr,kp,AR31+1

! Draw I section at beginning of beam
k,DRAW_IBEAM_ARR_KPNUMS(1), 0, 0,      0-arg2/2
k,DRAW_IBEAM_ARR_KPNUMS(2), 0, 0,      0
k,DRAW_IBEAM_ARR_KPNUMS(3), 0, 0,      0+arg2/2
k,DRAW_IBEAM_ARR_KPNUMS(4), 0, 0+arg3, 0-arg2/2
k,DRAW_IBEAM_ARR_KPNUMS(5), 0, 0+arg3, 0
k,DRAW_IBEAM_ARR_KPNUMS(6), 0, 0+arg3, 0+arg2/2
k,DRAW_IBEAM_ARR_KPNUMS(7), 0, arg3/2, 0

l, DRAW_IBEAM_ARR_KPNUMS(1), DRAW_IBEAM_ARR_KPNUMS(2)
l, DRAW_IBEAM_ARR_KPNUMS(2), DRAW_IBEAM_ARR_KPNUMS(3)
l, DRAW_IBEAM_ARR_KPNUMS(4), DRAW_IBEAM_ARR_KPNUMS(5)
l, DRAW_IBEAM_ARR_KPNUMS(5), DRAW_IBEAM_ARR_KPNUMS(6)
l, DRAW_IBEAM_ARR_KPNUMS(2), DRAW_IBEAM_ARR_KPNUMS(7)
l, DRAW_IBEAM_ARR_KPNUMS(7), DRAW_IBEAM_ARR_KPNUMS(5)

cm,AR29,line

! Draw axis line
lsel,u,line,,all
k,DRAW_IBEAM_ARR_KPNUMS(8),  arg1, 0, -arg2/2
lstr,DRAW_IBEAM_ARR_KPNUMS(1),DRAW_IBEAM_ARR_KPNUMS(8)
AR32=lsnext(0)

! Extrude I section to create areas
cmsel,s,AR29
*get,AR31,line,,count
AR22=0
*do,AR21,1,AR31,1
    cmsel,s,AR29
    AR22=lsnext(AR22)
    lsel,s,line,,AR22
    lsel,a,line,,AR32
    adrag,AR22,,,,,,AR32
*enddo
lsla,s
lsel,a,line,,AR32
ldele,AR32,,,1

lsla,s
ksll,s
nummrg,kp

cm,AR28,kp
cm,AR29,line
cm,AR30,AREA

! Create flanges selection component
asel,s,loc,y,0
asel,a,loc,y,arg3
cmsel,r,ar30
cm,DRAW_IBEAM_COMPA_FLANGES,area

! Create web selection component
asel,s,loc,z,0
cmsel,r,AR30
cm,DRAW_IBEAM_COMPA_WEB,area

krestore
lrestore
arestore
cmsel,a,DRAW_IBEAM_COMPA_WEB
cmsel,a,DRAW_IBEAM_COMPA_FLANGES
cmsel,a,AR28
cmsel,a,AR29

! Clear
cmdele,AR28
cmdele,AR29
cmdele,AR30
*del,DRAW_IBEAM_ARR_KPNUMS

