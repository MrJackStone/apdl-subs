! DRAW_IBEAM , L    , bf   , dm   , compFlanges , compWeb
! DRAW_IBEAM , arg1 , arg2 , arg3 , arg4        , arg5

! Description:
! Draws the six areas of a doubly-symmetric I beam in the active coordinate system (web and each
! flange split into two areas).
! Length is drawn along X, depth along Y, and width along Z. The origin of the active CSYS is placed
! at the intersection between the bottom flange and the web, at the beginning of the beam.
! The created areas are stored in area selection components named as follows:
!    Flange areas:  COMPA_<flangeSuffix>
!    Top flange:    COMPA_<flangeSuffix>_TOP
!    Bottom flange: COMPA_<flangeSuffix>_BOT
!    Web areas:     COMPA_<webSuffix>
! Note that the provided suffixes should not be longer than 22 characters and:
!    <flangeSuffix> (arg4) defaults to 'IBEAM_FLANGES'
!    <webSuffix>    (arg5) defaults to 'IBEAM_WEB'

! Arguments:
! arg1: L            -  span length (X)
! arg2: bf           -  flange width (Z)
! arg3: dm           -  section depth (Y)
! arg4: compFlanges  -  flange area selection component suffix/infix
! arg5: compWeb      -  web area selection component suffix

! Local parameters:
! AR20: parType
! AR28: COMPK_NEW
! AR29: COMPL_NEW
! AR30: COMPA_NEW
! AR34: flangeCompName
! AR35: webCompName
! AR36: topFlangeCompName
! AR37: botFlangeCompName

! Global parameters (erased at the end of this subroutine):
! -

! Output parameters:
!   COMPA_<flangeSuffix>      (defaults to COMPA_IBEAM_FLANGES)
!   COMPA_<webSuffix>         (defaults to COMPA_IBEAM_WEB)
!   COMPA_<flangeSuffix>_TOP  (defaults to COMPA_IBEAM_FLANGES_TOP)
!   COMPA_<flangeSuffix>_BOT  (defaults to COMPA_IBEAM_FLANGES_BOT)


!! Prepare environment                                                                            !!
pbackup
mpopbackup
kbackup
lbackup
abackup
/prep7
/uis,MSGPOP,3
ksel,u,kp,,all
lsel,u,line,,all
asel,u,area,,all

!! Set name of output selection components                                                        !!
! Flange areas
*get,AR20,parm,arg4,type
AR34='COMPA_IBEAM_FLANGES'
*if,AR20,eq,3,then
  AR34=strcat('COMPA_',arg4)
  AR36=strcat(AR34,'_TOP')
  AR37=strcat(AR34,'_BOT')
*endif

! Web areas
*get,AR20,parm,arg5,type
AR35='COMPA_IBEAM_WEB'
*if,AR20,eq,3,then
  AR35=strcat('COMPA_',arg5)
*endif

!! Draw merged areas                                                                              !!
! Bottom flange: right half (+Z)
wpcsys,-1
wprota,,90
blc4,0,0,arg1,arg2/2

! Bottom flange: left half (-Z)
wpcsys,-1
wprota,,-90
blc4,0,0,arg1,arg2/2

! Top flange
agen,2,all,,,,arg3

! Web: bottom half
wpcsys,-1
blc4,0,0,arg1,arg3/2

! Web: top half
wpcsys,-1
wpoffs,,arg3/2
blc4,0,0,arg1,arg3/2

! Merge new areas
nummrg,kp

!! Create selection components                                                                    !!
! New solid model entities
cm,AR28,kp
cm,AR29,line
cm,AR30,area

! Top flange
cmsel,s,AR30
asel,r,loc,y,arg3
cm,%AR36%,area

! Bottom flange
cmsel,s,AR30
asel,r,loc,y,0
cm,%AR37%,area

! Both flanges
cmsel,s,%AR36%
cmsel,a,%AR37%
cm,%AR34%,area

! Web
cmsel,s,AR30
asel,r,loc,z,0
cm,%AR35%,area

!! Clean up                                                                                       !!
wpcsys,-1
krestore
lrestore
arestore
mpoprestore
prestore
cmsel,a,AR28
cmsel,a,AR29
cmsel,a,AR30
cmdele,AR28
cmdele,AR29
cmdele,AR30
