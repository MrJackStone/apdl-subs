! SM_PILESUP_RETWALL, wallCsys, geom_params, selCompInfix
! SM_PILESUP_RETWALL,     arg1,        arg2,         arg3

! Description:
! Draws solid model entities (keypoints, lines, areas) representing a retention wall supported by
! a pile cap connected to piles.

! Arguments:
! arg1: wallCsys      -  local coordinate system, origin at the bottom of the wall
! arg2: geom_params   -  name of array containing the values of the geometric parameters that define
!                        this structure. Parameters should be in the following order in the array:
!                           POS  NAME         DESCRIPTION
!                             1  wallLength   length of the wall (local Y)
!                             2  wallHeight   height of the wall at Y=0
!                             3  wallToe      height of the wall at Y=length
!                             4  capLength    length of the pile cap (local Y)
!                             5  capWidth     width of the pilar cap (local X)
!                             6  capThk       thickness of the pile cap
!                                             (along Z, optional: separates pile top from pile cap)
!                             7  pileLength   length of the piles
!                                             (not including cap thickness)
!                             8  pileRows     number of pile rows
!                                             (piles in each row have equal Y)
!                             9  pileCols     number of piles columns
!                                             (piles in each column have equal X)
!                            10  pileOffset   distance between center of pile and edge of pile cap,
!                                             both in the X and Y local directions
! arg3: selCompInfix  -  string to be used in the middle of the output component's name as follows:
!                           COMPx_<infix>_<name>
!                        This infix has to be at most 10 characters long and defaults to blank.

! Local parameters:
! AR21: wallLength
! AR22: wallHeight
! AR23: wallToe
! AR24: capLength
! AR25: capWidth
! AR26: capThk
! AR27: pileLength
! AR28: pileRows
! AR29: pileCols
! AR30: pileOffset
! AR31: capOffset
! AR32: pileGroupWidth
! AR33: pileXSpacing
! AR34: pileGroupLength
! AR35: pileYSpacing
! AR36: minPileY
! AR37: nthPile
! AR38: nthPileRow
! AR39: pileY
! AR40: nthPileCol
! AR41: pileX
! AR42: refKP

! Global parameters (erased at the end of this subroutine)
! PILESUPRETWALL_ARR_pileX
! PILESUPRETWALL_ARR_pileY
! PILESUPRETWALL_ARR_KPnums
! PILESUPRETWALL_VAR_topKP
! PILESUPRETWALL_VAR_botKP

! Output parameters:
!   Without infix (default):  |  With infix:
!   COMPK_pileTop             |  COMPK_<infix>_pileTop
!   COMPK_pileBot             |  COMPK_<infix>_pileBot
!   COMPL_piles               |  COMPL_<infix>_piles
!   COMPL_pileConnections     |  COMPL_<infix>_pileConnections
!   COMPA_cap                 |  COMPA_<infix>_cap
!   COMPA_wall                |  COMPA_<infix>_wall


! Extract values of geometric parameters from array
AR21 = %arg2%(1)
AR22 = %arg2%(2)
AR23 = %arg2%(3)
AR24 = %arg2%(4)
AR25 = %arg2%(5)
AR26 = %arg2%(6)
AR27 = %arg2%(7)
AR28 = %arg2%(8)
AR29 = %arg2%(9)
AR30 = %arg2%(10)

! Calculated geometric params:
AR31=(AR24-AR21)/2

AR32=AR25-2*AR30
AR33=AR32/(AR29-1)

AR34=AR24-2*AR30
AR35=AR34/(AR28-1)
AR36=(AR21-AR34)/2

*del,PILESUPRETWALL_ARR_pileX,,nopr
*dim,PILESUPRETWALL_ARR_pileX,,AR29
*vfill,PILESUPRETWALL_ARR_pileX(1),ramp,-AR32/2,AR33

*del,PILESUPRETWALL_ARR_pileY,,nopr
*dim,PILESUPRETWALL_ARR_pileY,,AR28
*vfill,PILESUPRETWALL_ARR_pileY(1),ramp,AR36,AR35

! Activate wall's local CSYS
csbackup
csys,arg1

! Backup current selection
kbackup
lbackup
abackup

! Initialize output selection components
ksel,u,kp,,all
lsel,u,line,,all
asel,u,area,,all
cmpadd,'COMPK_ref'
cmpadd,'COMPK_top'
cmpadd,'COMPK_bot'
cmpadd,'COMPL_piles'
cmpadd,'COMPL_pileConnections'
cmpadd,'COMPA_cap'
cmpadd,'COMPA_wall'

! Generate keypoint numbers
nextkp,'PILESUPRETWALL_ARR_KPnums',8

! Draw wall
k , PILESUPRETWALL_ARR_KPnums(1) , 0 , 0    , 0
k , PILESUPRETWALL_ARR_KPnums(2) , 0 , AR21 , 0
k , PILESUPRETWALL_ARR_KPnums(3) , 0 , AR21 , AR23
k , PILESUPRETWALL_ARR_KPnums(4) , 0 , 0    , AR22
asel,u,area,,all
a,PILESUPRETWALL_ARR_KPnums(1:4)
cm,COMPA_wall,area

! Draw pile cap
k , PILESUPRETWALL_ARR_KPnums(5) , -AR25/2 , -AR31     , 0
k , PILESUPRETWALL_ARR_KPnums(6) , AR25/2  , -AR31     , 0
k , PILESUPRETWALL_ARR_KPnums(7) , AR25/2  , AR24-AR31 , 0
k , PILESUPRETWALL_ARR_KPnums(8) , -AR25/2 , AR24-AR31 , 0
asel,u,area,,all
a,PILESUPRETWALL_ARR_KPnums(5:8)
cm,COMPA_cap,area

! Slice cap at wall
cmsel,s,COMPA_cap
csslicexz,0
csslicexz,AR21
cm,COMPA_cap,area

! Slice cap at piles
cmsel,s,COMPA_cap
wpcsys,,arg1
batch_slicexz,'PILESUPRETWALL_ARR_pileY',1,0
cm,COMPA_cap,area

! Slice wall at piles
cmsel,s,COMPA_wall
wpcsys,,arg1
batch_slicexz,'PILESUPRETWALL_ARR_pileY',1,0
cm,COMPA_wall,area

! Merge cap & wall
cmsel,s,COMPA_cap
cmsel,a,COMPA_wall
lsla,s
ksll,s
nummrg,kp
cm,COMPK_temp,kp

! Draw piles
AR37=0
*do,AR38,1,AR28,1
  AR39=PILESUPRETWALL_ARR_pileY(AR38)

  *do,AR40,1,AR29,1
    AR41=PILESUPRETWALL_ARR_pileX(AR40)
    AR37=AR37+1

    cmsel,s,COMPK_temp
    AR42=kp(AR41,AR39,0)
    ksel,s,kp,,AR42
    cmpadd,'COMPK_ref'

    nextkp,'PILESUPRETWALL_VAR_topKP',1
    ksel,u,kp,,all
    k,PILESUPRETWALL_VAR_topKP,AR41,AR39,-AR26
    cmpadd,'COMPK_top'

    nextkp,'PILESUPRETWALL_VAR_botKP',1
    ksel,u,kp,,all
    k,PILESUPRETWALL_VAR_botKP,AR41,AR39,-AR26-AR27
    cmpadd,'COMPK_bot'

    ksel,s,kp,,AR42
    ksel,a,kp,,PILESUPRETWALL_VAR_topKP
    ksel,a,kp,,PILESUPRETWALL_VAR_botKP

    lsel,u,line,,all
    l,PILESUPRETWALL_VAR_topKP,PILESUPRETWALL_VAR_botKP
    cm,COMPL_pile%AR37%,line
    cmpadd,'COMPL_piles'

    lsel,u,line,,all
    l,AR42,PILESUPRETWALL_VAR_topKP
    cm,COMPL_pileConnection%AR37%,line
    cmpadd,'COMPL_pileConnections'

  *enddo
*enddo

! Clean up
csrestore
krestore
lrestore
arestore
cmsel,a,'COMPK_top'
cmsel,a,'COMPK_bot'
cmsel,a,'COMPL_piles'
cmsel,a,'COMPL_pileConnections'
cmsel,a,'COMPA_cap'
cmsel,a,'COMPA_wall'
*del,PILESUPRETWALL_ARR_KPnums,,nopr
*del,PILESUPRETWALL_ARR_pileX,,nopr
*del,PILESUPRETWALL_ARR_pileY,,nopr
*del,PILESUPRETWALL_VAR_topKP,,nopr
*del,PILESUPRETWALL_VAR_botKP,,nopr
