! SEC_CTUBE , coreDiameter , pipeThk , coreMat , pipeMat , edgeSize , name
! SEC_CTUBE , ARG1         , ARG2    , ARG3    , ARG4    , ARG5     , ARG6

! Description:
! Writes a custom beam cross-section shape (circle enveloped by ring) to file. Different materials
! can be assigned to the circle (core) and the pipe (ring).

! Arguments:
! ARG1: coreDiameter  -  diameter of the core (inner circle)
! ARG2: pipeThk       -  thickness of the outer ring's wall
! ARG3: coreMat       -  material assigned to the core
! ARG4: pipeMat       -  material assigned to the ring
! ARG5: edgeSize      -  size of cross-section element edge
!                        (defaults to outer perimeter divided by 16)
! ARG6: name          -  name assigned to the cross-section

! Local parameters:
! ar21: pi
! ar22: outerDiameter
! ar23: outerPerimeter
! ar24: edgeLength
! ar25: edgeDivs
! ar26: angSize
! ar31: newET
! ar32: newCS
! ar33: COMPA_section
! ar34: COMPA_core
! ar35: COMPA_ring
! ar36: COMPL_arcs
! ar37: COMPL_straight


! Prepare environment
smdbackup , 'NONE'
fembackup , 'NONE'
pcbackup
csbackup
afunbackup
mpopbackup

/uis,msgpop,3
/prep7
*afun,rad

newET = etyiqr(0,16)
et,newET,82

newCS = (csyiqr(0,14)>11)
cswpla,newCS,cylin

! Parse inputs
pi             = acos(-1)
outerDiameter  = ARG1+(2*ARG2)
outerPerimeter = pi*outerDiameter

edgeLength = outerPerimeter/16
*if,ARG5,gt,0,then
  edgeLength = ARG5
*endif

edgeDivs = nint(outerPerimeter/edgeLength)
angSize  = 360/edgeDivs

! Draw cross-section shape
cyl4,0,0,ARG1/2
cyl4,0,0,ARG1/2,outerDiameter/2

wprota,,,90
asbw,all,,delete
wprota,,-90

wprota,,90
asbw,all,,delete
wprota,,-90

nummrg,kp

! Create selection components
cm,COMPA_section,area

asel,r,loc,x,0,ARG1/2
cm,COMPA_core,area

cmsel,s,COMPA_section
asel,r,loc,x,ARG1/2,outerDiameter/2
cm,COMPA_ring,area

cmsel,s,COMPA_section
lsel,s,loc,x,ARG1/2
lsel,a,loc,x,outerDiameter/2
lsla,r
cm,COMPL_arcs,line

cmsel,s,COMPA_section
lsla
cmsel,u,COMPL_arcs
cm,COMPL_straight,line

! Mesh cross-section
csys,newCS

cmsel,s,COMPA_core
aatt,ARG3,,newET

cmsel,s,COMPA_ring
aatt,ARG4,,newET

cmsel,s,COMPL_arcs
lesize,all,,angSize

cmsel,s,COMPL_straight
lesize,all,edgeLength

cmsel,s,COMPA_section
amesh,all

! Write cross-section file
secwrite,ARG6,sect,,newET

! Clean up
csrestore

cmsel,s,COMPA_section
lsla
ksll

cmdele,COMPA_section
cmdele,COMPA_core
cmdele,COMPA_ring

aclear,all
adele,all,,,1

etdele,newET
csdele,newCS

smdrestore
femrestore
pcrestore
afunrestore
mpoprestore
