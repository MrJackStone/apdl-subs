! SEC_CTUBE , coreDiameter , pipeThk , coreMat , pipeMat , edgeSize , name
! SEC_CTUBE , ARG1         , ARG2    , ARG3    , ARG4    , ARG5     , ARG6

! Description:
! Writes a custom beam cross-section shape (circle enveloped by ring) to file. Different materials
! can be assigned to the circle (core) and the pipe (ring).

! Arguments:
! ARG1: coreDiameter  -  diameter of the core (inner circle)
! ARG2: pipeThk       -  thickness of the outer ring's wall
! ARG3: coreMat       -  material assigned to the core
! ARG4: pipeMat       -  material assigned to the ring
! ARG5: edgeSize      -  size of cross-section element edge
!                        (defaults to outer perimeter divided by 16)
! ARG6: name          -  name assigned to the cross-section

! Local parameters:
! ar21: pi
! ar22: outerDiameter
! ar23: outerPerimeter
! ar24: edgeLength
! ar25: edgeDivs
! ar26: angSize
! ar31: newET
! ar32: newCY
! ar33: COMPA_section
! ar34: COMPA_core
! ar35: COMPA_ring
! ar36: COMPL_arcs
! ar37: COMPL_straight
! ar38: newCS


! Prepare environment
smdbackup , 'NONE'
fembackup , 'NONE'
pcbackup
csbackup
afunbackup
mpopbackup

/uis,msgpop,3
/prep7
*afun,rad

ar31 = etyiqr(0,16)
et,ar31,82

ar38 = (csyiqr(0,14)>11)
ar32 = ar38+1
cswpla,ar38,cart
cswpla,ar32,cylin

csys,ar38

! Parse inputs
ar21 = acos(-1)
ar22 = ARG1+(2*ARG2)
ar23 = ar21*ar22

ar24 = ar23/16
*if,ARG5,gt,0,then
  ar24 = ARG5
*endif

ar25 = nint(ar23/ar24)
ar26 = 360/ar25

! Draw cross-section shape
cyl4 , 0 , 0 , ARG1/2
cyl4 , 0 , 0 , ARG1/2 ,, ar22/2

wprota,,,90
asbw,all,,delete
wprota,,,-90

wprota,,90
asbw,all,,delete
wprota,,-90

nummrg,kp

! Create selection components
csys,ar32

cm,ar33,area

lsel,r,loc,x,0,ARG1/2
asll,r,1
cm,ar34,area

cmsel,s,ar33
lsla
lsel,r,loc,x,ARG1/2,ar22/2
asll,r,1
cm,ar35,area

cmsel,s,ar33
lsel,s,loc,x,ARG1/2
lsel,a,loc,x,ar22/2
lsla,r
cm,ar36,line

cmsel,s,ar33
lsla
cmsel,u,ar36
cm,ar37,line

csys,ar38

! Mesh cross-section
cmsel,s,ar34
aatt,ARG3,,ar31

cmsel,s,ar35
aatt,ARG4,,ar31

cmsel,s,ar36
lesize,all,,ar26

cmsel,s,ar37
lesize,all,ar24

cmsel,s,ar33
amesh,all

! Write cross-section file
secwrite,ARG6,sect,,ar31

! Clean up
csrestore

cmsel,s,ar33
lsla
ksll

cmdele,ar33
cmdele,ar34
cmdele,ar35
cmdele,ar36
cmdele,ar37

aclear,all
adele,all,,,1

etdele,ar31
csdele,ar32
csdele,ar38

smdrestore
femrestore
pcrestore
afunrestore
mpoprestore
