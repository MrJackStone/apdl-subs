! csrestore
! CSRESTORE

! Description:
! Restores the value of the active coordinate system previously stored by CSBACKUP.

! Arguments:
! -

! Local parameters:
! ar20: parType
! ar21: length
! ar22: restoredValue

! Global parameters:
! CSBACKUP_

! Global parameters (erased at the end of this subroutine):
! CSBACKUP_ARR_temp


*get,ar20,parm,CSBACKUP_,type

! Abort if no record is found
*if,ar20,ne,1,then
  *msg,warn
CSRESTORE error: no stored CSYS number to restore.%/&
CSRESTORE command is ignored.
  *return,-1
*endif

! Get value to restore
*get,ar21,parm,CSBACKUP_,dim,x
ar22 = CSBACKUP_(ar21)

! Update array that stores values
*if,ar21,eq,1,then
  *del,CSBACKUP_,,nopr
*else
  *del  , CSBACKUP_ARR_temp ,      , nopr
  *dim  , CSBACKUP_ARR_temp ,      , ar21-1
  *vfun , CSBACKUP_ARR_temp , copy , CSBACKUP_
  *del  , CSBACKUP_         ,      , nopr
  *dim  , CSBACKUP_         ,      , ar21-1
  *vfun , CSBACKUP_         , copy , CSBACKUP_ARR_temp
  *del  , CSBACKUP_ARR_temp ,      , nopr
*endif

! Restore value
csys,ar22
