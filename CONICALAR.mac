! conicalar , rbot , rtop , z1   , z2   , theta1 , theta2
! CONICALAR , ARG1 , ARG2 , ARG3 , ARG4 , ARG5   , ARG6

! Description:
! Creates conical areas by applying the native 'CONE' command and deleting unwanted volumes
! and areas.

! Arguments (see full description in ANSYS's help page for command 'CONE')
! ARG1: RBOT    -  radius at the bottom
! ARG2: RTOB    -  radius at the top
! ARG3: Z1      -  z coordinate at the bottom
! ARG4: Z2      -  z coordinate at the top
! ARG5: THETA1  -  starting angle at the bottom
! ARG6: THETA2  -  ending angle at the bottom

! Local parameters:
! ar20: VNum
! ar21: A1
! ar22: A2
! ar23: A3
! ar24: A4
! ar25: ACount

! Output parameters:
! CONICALAR_COMPA_cone


! Prepare environment
smdbackup,'none'

! Draw areas
*if,abs(ARG3-ARG4),le,1e-6,then
  ! Flat circular area:
  wpoffs,,,ARG3
  cyl4,0,0,(ARG1>ARG2)
  wprota,,90
  asbw,all,,delete
  wprota,,-90
  wpoffs,,,-ARG3

*else
  ! Conical area:
  cone, ARG1, ARG2, ARG3, ARG4, ARG5, ARG6
  *get,ar20,volu,,num,min
  vdele,ar20,,,0

  *get,ar25,area,,count

  ar21 = arnext(0)
  ar22 = arnext(ar21)
  ar23 = arnext(ar22)
  ar24 = arnext(ar23)

  *if,ar25,eq,3,then
    asel,s,area,,ar21
    adele,all,,,1

    asel,s,area,,ar22
    asel,a,area,,ar23

  *elseif,ar25,eq,4,then
    asel,s,area,,ar21
    asel,a,area,,ar22
    adele,all,,,1

    asel,s,area,,ar23
    asel,a,area,,ar24

  *endif

*endif

cm,CONICALAR_COMPA_cone,area

! Clean up
smdrestore
cmsel,a,CONICALAR_COMPA_cone
