! cncthsx , outputTable , matNum , curveDivs
! CNCTHSX , ARG1        , ARG2   , ARG3

! Description:
! Fills a table with concrete's temperature-strain curve. Optionally  applies this property to a
! given material.

! Arguments:
! ARG1: outputTable  -  name of param to which the temperature-strain table will be assigned
!                       (defautls to 'CNCTHSX_ARR_temp_strain')
! ARG2: matNum       -  index of material to which the thermal property should be applied:
!                         [0]    do not assign to any material
!                         [N>0]  assign to material N
! ARG3: curveDivs    -  number of divisions applied to the continuous temperature-strain curve
!                       to discretize it (defaults to 20)

! Local parameters:
! ar20: parType
! ar21: tempStrainTableParmName
! ar22: matIndex
! ar23: curveDivisions
! ar24: temperatureIncrement

! Global erased parameters:
!   CNCTHSX_ARR_temp1
!   CNCTHSX_ARR_temp2

! Output parameters:
!   %ARG1% (defaults to 'CNCTHSX_ARR_temp_strain')


! Parse inputs
ar21 = 'CNCTHSX_ARR_temp_strain'
*get,ar20,parm,ARG1,type
*if,ar20,eq,3,then
  ar21 = ARG1
*endif

ar22 = 0
*if,ARG2,gt,0,then
  ar22 = ARG2
*endif

ar23 = 20
*if,ARG3,gt,0,then
  ar23 = ARG3
*endif

! Prepare environment
*del , CNCTHSX_ARR_temp1 ,  , nopr
*del , CNCTHSX_ARR_temp2 ,  , nopr
*del , %ARG1%            ,  , nopr
*dim , %ARG1%            ,  , ar23 , 2

! Calculate temperature table
ar24 = (700-20)/(ar23-2)

*vfill,%ARG1%(1,1),ramp,20,ar24

*vfun  , CNCTHSX_ARR_temp1 , pwr               , %ARG1%(1,1) , 3
*voper , CNCTHSX_ARR_temp1 , CNCTHSX_ARR_temp1 , mult        , 2.3e-11
*voper , CNCTHSX_ARR_temp2 , %ARG1%(1,1)       , mult        , 9e-5
*voper , CNCTHSX_ARR_temp2 , CNCTHSX_ARR_temp1 , add         , CNCTHSX_ARR_temp2
*voper , %ARG1%(1,2)       , CNCTHSX_ARR_temp2 , add         , -1.8e-4

%ARG1%(ar23,1) = 1200
%ARG1%(ar23,2) = 14e-3

! Set material properties
*if,ar22,gt,0,then
  mptemp
  mptemp,,%ar21%(1:ar23,1)
  mpdata,thsx,ar22,,%ar21%(1:ar23,2)
*endif

! Clean up
*del , CNCTHSX_ARR_temp1 ,  , nopr
*del , CNCTHSX_ARR_temp2 ,  , nopr
