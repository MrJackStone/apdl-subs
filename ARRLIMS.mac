! arrlims, outputParm
! ARRLIMS,       ARG1

! Description:
! Calculates maximum and minimum values for each column of the given 2-D arrays.

! Arguments:
! ARG1: outputParm  -  name of output parameter to which min/max values will be written

! Implicit arguments:
! ARRLIMS_ARR_names  -  array of characters, each position containing the name of a 2-D array
!                       parameter (all must have an equal number of columns)

! Local parameters:
! ar20: arrcount
! ar21: totDim
! ar22: nthArr
! ar23: arrName
! ar24: arrDim
! ar25: colCount
! ar26: idx1
! ar27: nthCol
! ar28: minval
! ar29: maxval

! Output parameters:
! ARRLIMS_ARR_allRows  -  2-D array combining all input array rows


*get,ar20,parm,ARRLIMS_ARR_names,dim,y

ar21 = 0
*do,ar22,1,ar20,1
    ar23 = ARRLIMS_ARR_names(1,ar22)
    *get,ar24,parm,%ar23%,dim,x
    ar21 = ar21+ar24
    *get,ar25,parm,%ar23%,dim,y
*enddo

*del,ARRLIMS_ARR_allRows,,nopr
*dim,ARRLIMS_ARR_allRows,,ar21,ar25

ar26 = 1
*do,ar22,1,ar20,1
    ar23 = ARRLIMS_ARR_names(1,ar22)
    *get,ar24,parm,%ar23%,dim,x
    *do,ar27,1,ar25,1
        *vfun,ARRLIMS_ARR_allRows(ar26,ar27),copy,%ar23%(1,ar27)
    *enddo
    ar26 = ar26+ar24
*enddo

*del,%ARG1%,,nopr
*dim,%ARG1%,,2,ar25
*do,ar27,1,ar25,1
    *vscfun,ar28,min,ARRLIMS_ARR_allRows(1,ar27)
    *vscfun,ar29,max,ARRLIMS_ARR_allRows(1,ar27)
    %ARG1%(1,ar27) = ar28
    %ARG1%(2,ar27) = ar29
*enddo
