! NDBBOX , xmin , xmax , ymin , ymax , zmin , zmax
! NDBBOX , arg1 , arg2 , arg3 , arg4 , arg5 , arg6

! Description:
! Obtains the bounding box coordinates of the current nodal selection in the active coordinate
! system.

! Arguments:
! arg1: xmin  -  name of the parameter to which the minimum value of X will be assigned
! arg2: xmax  -  name of the parameter to which the maximum value of X will be assigned
! arg3: ymin  -  name of the parameter to which the minimum value of Y will be assigned
! arg4: ymax  -  name of the parameter to which the maximum value of Y will be assigned
! arg5: zmin  -  name of the parameter to which the minimum value of Z will be assigned
! arg6: zmax  -  name of the parameter to which the maximum value of Z will be assigned

! Output parameters:
! %arg1% through %arg6% (default to: NDBBOX_VAR_xmin through NDBBOX_VAR_zmax)

! Local parameters:
! AR20: parType
! AR21: xminParm
! AR22: xmaxParm
! AR23: yminParm
! AR24: ymaxParm
! AR25: zminParm
! AR26: zmaxParm


! Set defaults
AR21='NDBBOX_VAR_xmin'
AR22='NDBBOX_VAR_xmax'
AR23='NDBBOX_VAR_ymin'
AR24='NDBBOX_VAR_ymax'
AR25='NDBBOX_VAR_zmin'
AR26='NDBBOX_VAR_zmax'

! Override with user-defined parameter names
*get,AR20,parm,arg1,type
*if,AR20,eq,3,then
  AR21=arg1
*endif
*get,AR20,parm,arg2,type
*if,AR20,eq,3,then
  AR22=arg2
*endif
*get,AR20,parm,arg3,type
*if,AR20,eq,3,then
  AR23=arg3
*endif
*get,AR20,parm,arg4,type
*if,AR20,eq,3,then
  AR24=arg4
*endif
*get,AR20,parm,arg5,type
*if,AR20,eq,3,then
  AR25=arg5
*endif
*get,AR20,parm,arg6,type
*if,AR20,eq,3,then
  AR26=arg6
*endif

! Get & assign values
*get,%AR21%,node,,mnloc,x
*get,%AR22%,node,,mxloc,x
*get,%AR23%,node,,mnloc,y
*get,%AR24%,node,,mxloc,y
*get,%AR25%,node,,mnloc,z
*get,%AR26%,node,,mxloc,z
