! ARCONE, RBOT, RTOP,   Z1,   Z2, THETA1, THETA2
! ARCONE, arg1, arg2, arg3, arg4,   arg5,   arg6

! Description:
! Creates conical areas by applying the native 'CONE' command and deleting unwanted volumes
! and areas.

! Arguments (see full description in ANSYS's help page for command 'CONE')
! arg1: RBOT    -  radius at the bottom
! arg2: RTOB    -  radius at the top
! arg3: Z1      -  z coordinate at the bottom
! arg4: Z2      -  z coordinate at the top
! arg5: THETA1  -  starting angle at the bottom
! arg6: THETA2  -  ending angle at the bottom

! Local parameters:
! AR20: VNum
! AR21: A1
! AR22: A2
! AR23: A3
! AR24: A4

! Output parameters:
! ARCONE_COMPA_cone

KBACKUP
LBACKUP
ABACKUP
VBACKUP

VSEL,U,VOLU,,ALL
ASEL,U,AREA,,ALL
LSEL,U,LINE,,ALL
KSEL,U,KP,,ALL

CONE, arg1, arg2, arg3, arg4, arg5, arg6
*get,VNum,VOLU,,NUM,MIN
VDELE,VNum,,,0

AR21=ARNEXT(0)
AR22=ARNEXT(AR21)
AR23=ARNEXT(AR22)
AR24=ARNEXT(AR23)

ASEL,S,AREA,,AR21
ASEL,A,AREA,,AR24
ADELE,ALL,,,1

ASEL,S,AREA,,AR22
ASEL,A,AREA,,AR23
CM,ARCONE_COMPA_cone,AREA

KRESTORE
LRESTORE
ARESTORE
VRESTORE

CMSEL,A,ARCONE_COMPA_cone
