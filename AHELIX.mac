! AHELIX
! radius = arg1
! phi = arg2
! width = arg3
! direction = arg4
! theta0 = arg5


radius=arg1
phi=arg2
width=arg3
direction=arg4
theta0=arg5

KBACKUP
LBACKUP
ABACKUP

ASEL,U,AREA,,ALL
LSEL,U,LINE,,ALL
KSEL,U,KP,,ALL

*afun,RAD
pi=ACOS(-1)
*afun,DEG
pitch=2*PI*radius/TAN(phi)
*afun,RAD
beta=ATAN(-2*pi*radius/pitch)

/prep7

! First helical line
HELIX,radius,phi,1,direction,theta0
CM,COMPL_helix1,LINE
CM,COMPK_helix1,KP

CMSEL,S,COMPK_helix1
KSEL,R,LOC,Z,0*pitch/4
*get,H1_K1,KP,,NUM,MIN

CMSEL,S,COMPK_helix1
KSEL,R,LOC,Z,1*pitch/4
*get,H1_K2,KP,,NUM,MIN

CMSEL,S,COMPK_helix1
KSEL,R,LOC,Z,2*pitch/4
*get,H1_K3,KP,,NUM,MIN

CMSEL,S,COMPK_helix1
KSEL,R,LOC,Z,3*pitch/4
*get,H1_K4,KP,,NUM,MIN

CMSEL,S,COMPK_helix1
KSEL,R,LOC,Z,4*pitch/4
*get,H1_K5,KP,,NUM,MIN

CMSEL,S,COMPL_helix1
CMSEL,S,COMPK_helix1
*get,KP1,KP,,NUM,MIN
KSEL,S,KP,,KP1
LSLK,S,0
KSLL,S
*get,KP2,KP,,NUM,MAX
*get,KMaxD,KP,,NUM,MAXD
KPahead=KMaxD+1
KPbehind=KMaxD+2
CMSEL,S,COMPL_helix1
CMSEL,S,COMPK_helix1

beta=ATAN(-2*pi*radius/pitch)*180/pi
ellipseWidth=ABS(2*radius/COS(beta*pi/180))

KWPAVE,KP2
WPOFFS,,-radius
WPROTA,,,beta
CSWPLA,20,CART
K,KPahead,ellipseWidth/2,0,0
K,KPbehind,-ellipseWidth/2,0,0

CSWPLA,40,CYLIN,radius/(ellipseWidth/2)
LSEL,U,LINE,,ALL
L,KP2,KPahead
*get,Lahead,LINE,,NUM,MIN
LSEL,U,LINE,,ALL
L,KP2,KPbehind
*get,Lbehind,LINE,,NUM,MIN

CSYS,0
WPCSYS

LSEL,S,LINE,,Lahead
LSEL,A,LINE,,Lbehind
LSUM
*get,LAlen,LINE,Lahead,LENG
*get,LBlen,LINE,Lbehind,LENG
*get,KMaxD,KP,,NUM,MAXD
KDivA=KMaxD+1
KDivB=KMaxD+2
LDIV,Lahead,width/(2*LAlen),KDivA,,0
LDIV,Lbehind,width/(2*LBlen),KDivB,,0

thetaA=ACOS(KX(KDivA)/radius)
thetaB=ACOS(KX(KDivB)/radius)
theta0ahead=(2*pi*KZ(KDivA)/pitch)-thetaA
theta0behind=(2*pi*KZ(KDivB)/pitch)-thetaB

LDELE,ALL,,,1
CMSEL,S,COMPL_helix1
CMSEL,S,COMPK_helix1

CSYS,0

*get,LCount,LINE,,COUNT
*do,i,1,LCount-1,1
    *get,L1,LINE,,NUM,MIN
    LSEL,S,LINE,,L1
    KSLL,S
    LSLK,S
    *get,L2,LINE,,NUM,MAX
    LCOMB,L1,L2,DELETE
*enddo
*get,HelicalLine,LINE,,NUM,MIN
*get,KPMaxD,KP,,NUM,MAXD
KAux1=KPMaxD+1
KAux2=KPMaxD+2
KAux3=KPMaxD+3
KAux4=KPMaxD+4

K,KAux1,0,0,0
K,KAux2,radius,0,0
K,KAux3,radius*COS(theta0ahead),radius*SIN(theta0ahead),0
K,KAux4,radius*COS(theta0behind),radius*SIN(theta0behind),0
LSEL,U,LINE,,ALL
LARC,KAux2,KAux3,KAux1,radius
*get,Path1,LINE,,NUM,MIN
LSEL,U,LINE,,ALL
LARC,KAux2,KAux4,KAux1,radius
*get,Path2,LINE,,NUM,MIN

LSEL,S,LINE,,HelicalLine
LSEL,A,LINE,,Path1
LSEL,A,LINE,,Path2

ASEL,U,AREA,,ALL
ADRAG,HelicalLine,,,,,,Path1
*get,A1,AREA,,NUM,MIN
ASEL,U,AREA,,ALL
ADRAG,HelicalLine,,,,,,Path2
*get,A2,AREA,,NUM,MIN

ASEL,S,AREA,,A1
ASEL,A,AREA,,A2
ANORM,A2,1



