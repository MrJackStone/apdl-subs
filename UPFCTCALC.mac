! UPFCTCALC , outputParm , targetDisp , firstNset , lastNset , captureImages , folder , imgNamePrefix
! UPFCTCALC , arg1       , arg2       , arg3      , arg4     , arg5          , arg6   , arg7

! Description:
! Calculates the geometry imperfection import factor to be used in UPGEOM for a given interval of
! result sets.

! Arguments:
! arg1: outputParm     -  name of array parameter to which results will be written
!                         (defaults to UPFCTCALC_ARR_factors)
! arg2: targetDisp     -  desired imperfection magnitude to apply
! arg3: firstNset      -  number of first result set to be scanned
! arg4: lastNSet       -  number of last result set to be scanned
! arg5: captureImages  -  saves images of each mode's deformed shape to external PNG file
!                           [0] disable (default)
!                           [1] enable
! arg6: folder         -  name of the folder in which the files will be saved (optional).
!                         If a folder name is given, the folder will be created inside the current
!                         working directory. Defaults to the current working directory.
! arg7: imgNamePrefix  -  string used as prefix to the name of the exported image files.
!                         Defaults to 'UPFCTCALC'. Applies only if image capture is enabled,
!                         i.e., arg5=1.

! Local parameters:
! AR20: parType
! AR21: outputParmName
! AR22: nsetCount
! AR23: firstValidSet
! AR24: lastValidSet
! AR25: factor
! AR26: nthNset
! AR27: fnamePrefix

! Global parameters (erased at the end of this subroutine):
! UPFCTCALC_VAR_nthMax


! Set prefix of image file name
AR27='UPFCTCALC'
*get,AR20,parm,arg7,type
*if,AR20,eq,3,then
  AR27=arg7
*endif

! Get name of output parameter
AR21='UPFCTCALC_ARR_factors'
*get,AR20,parm,arg1,type
*if,AR20,eq,3,then
  AR21=arg1
*endif

! Calculate number of result sets to scan
AR23=arg3
AR24=(AR23>arg4)
AR22=AR24-AR23+1

! Initialize array to hold import factors
*del,%AR21%,,nopr
*dim,%AR21%,,AR22

! Loop over result sets and collect import factors
*do,AR26,AR23,AR24,1
  ! Read set, plot displacement vector sum
  set,,,,,,,AR26
  plnsol,u,sum
  cntrlims,,,'UPFCTCALC_VAR_nthMax'
  ! Capture image of deformed shape
  *if,arg5,eq,1,then
    takepic,'%AR27%_MODE_%AR26%',,,,,arg6
  *endif
  ! Set import factor to zero if max plotted value is also zero
  *if,UPFCTCALC_VAR_nthMax,ne,0,then
    AR25=arg2/UPFCTCALC_VAR_nthMax
  *else
    AR25=0
  *endif
  ! Store import factor
  %AR21%(AR26)=AR25
*enddo
