! PTHBYCS , pthName , nDiv , csStart , csEnd1 , csEnd2 , csEnd3 , csEnd4 , csEnd5 , csEnd6
! PTHBYCS , ARG1    , ARG2 , ARG3    , ARG4   , ARG5   , ARG6   , ARG7   , ARG8   , ARG9

! Description:
! Creates a path that starts at the origin of a CS and passses through the origins of up to 6
! other coordinate systems.

! Arguments:
! ARG1: pthName  -  name of the path to be created
! ARG2: nDiv     -  number of divisions in the path to be created
! ARG3: csStart  -  coordinate system that marks the start point of the path
! ARG4: csEnd1   -  coordinate system that marks the next path point
! ARG5: csEnd2   -  coordinate system that marks the next path point (optional)
! ARG6: csEnd3   -  coordinate system that marks the next path point (optional)
! ARG7: csEnd4   -  coordinate system that marks the next path point (optional)
! ARG8: csEnd5   -  coordinate system that marks the next path point (optional)
! ARG9: csEnd6   -  coordinate system that marks the next path point (optional)

! Local parameters:
! ar21: xi
! ar22: yi
! ar23: zi
! ar31: csNum
! ar32: ptCount
! ar33: nthPos

! Global erased parameters:
! PTHBYCS_ARR_csNums


! Parse inputs
*del,PTHBYCS_ARR_csNums,,nopr
*dim,PTHBYCS_ARR_csNums,,7
PTHBYCS_ARR_csNums(1) = ARG3 , ARG4 , ARG5 , ARG6 , ARG7 , ARG8 , ARG9

! Calculate number of path points
*do,ar33,1,7,1
  ar31 = PTHBYCS_ARR_csNums(ar33)
  *if,ar31,le,0,exit
  ar32 = ar33
*enddo

! Create path
path,%ARG1%,ar32,,ARG2

! Set path points
*do,ar33,1,ar32,1
  ar31 = PTHBYCS_ARR_csNums(ar33)

  *get,ar21,cdsy,ar31,loc,x
  *get,ar22,cdsy,ar31,loc,y
  *get,ar23,cdsy,ar31,loc,z

  ppath,ar33,,ar21,ar22,ar23
*enddo

! Clean up
*del,PTHBYCS_ARR_csNums,,nopr
