! STLPLATE , secNum , thk  , matNum , E    , fy   , Et   , fu   , dens , Poisson , offset
! STLPLATE , arg1   , arg2 , arg3   , arg4 , arg5 , arg6 , arg7 , arg8 , arg9    , ar10

! Description:
! Creates a shell-type cross-section and, optionally, a steel-like material model. The created
! material model will be either linear-elastic-isotropic (if fy=0) or bilinear-isotropic-hardening
! (if fy>0).

! Arguments:
! arg1: secNum   -  number of the shell cross-section to be created. This parameter can be of the
!                   following types:
!                      [INT]  > 0: ID number to be assigned to the newly created section;
!                             = 0: section number will be automatically chosen (lowest available);
!                      [STR]  ID automatically chosen (lowest available) and assigned to this
!                             string, which is interpreted as a parameter name
! arg2: thk      -  thickness of the shell's cross-section
! arg3: matNum   -  material number to be created and assigned to shell cross-section (default: 1)
!                   (if this number is negative the material will not be created, only assigned)
! arg4: E        -  steel elastic modulus (default: 2e11)
! arg5: fy       -  steel yield stress    (default: 0, linear-elastic material model)
! arg6: Et       -  steel tangent modulus (default: 0)
! arg7: fu       -  steel ultimate stress (default: fy)
! arg8: dens     -  mass density          (default: 7850)
! arg9: Poisson  -  Poisson's coefficient (default: 0.3)
! ar10: offset   -  applied offset to cross-section:
!                     [    0]  no offset (default, midplane)
!                     [  ó°¦Ž 0]  user-defined offset value
!                     ['BOT']  node offset to the bottom of the section
!                     ['TOP']  node offset to the top of the section
!

! Local parameters:
! AR20: parType
! AR21: newSecNumber
! AR22: maxSecNumber
! AR23: secNumOutputParm
! AR24: steelMatNum
! AR25: linearSteel

! Global parameters (erased at the end of this subroutine):

! Output parameters:
! %arg1% (defaults to 'STLPLATE_VAR_secNum')


pcbackup
/prep7

! Set number of cross-section
AR21=0
*get,AR20,parm,arg1,type
*if,AR20,eq,0,then
  AR21=arg1
*endif

*if,AR21,eq,0,then
  *get,AR22,secp,,num,max
  AR21=AR22+1
*endif

! Set name of output parameter to receive number of shell cross-section
*get,AR20,parm,arg1,type
AR23='STLPLATE_VAR_secNum'
*if,AR20,eq,3,then
  AR23=arg1
*endif
%AR23%=AR21

! Section offset option
AR26='MID'
AR27=0
*get,AR20,parm,ar10,type
*if,AR20,eq,0,then
  *if,ar10,ne,0,then
    AR26='USER'
    AR27=ar10
  *endif
*elseif,AR20,eq,3,then
  *if,upcase(ar10),eq,'BOT',then
    AR26='BOT'
  *elseif,upcase(ar10),eq,'TOP',then
    AR26='TOP'
  *endif
*endif

! Create steel material model:
AR24=(abs(arg3)>1)
*if,arg3,ge,0,then
  ! Determine if linear-elastic model is to be used
  AR25=0
  *if,arg5,le,0,then
    AR25=1
  *endif

  ! Create steel
  steel,AR24, arg4, arg5, arg6, arg7, arg8, arg9, AR25
*endif

! Create shell cross-section
sectype,AR21,'shell'
secdata,arg2,AR24
secoffset,%AR26%,AR27

! Clean up
pcrestore
