! lasttime , outputTime , outputSbst
! LASTTIME , ARG1       , ARG2

! Description:
! Returns the TIME value of the last converged substep.

! Arguments:
! ARG1: outputTime  -  name of the output parameter to which the last TIME value will be assigned
!                      (defaults to "LASTTIME_VAR_time")
! ARG2: outputSbst  -  name of the output parameter to which the last SBST value will be assigned
!                      (defaults to "LASTTIME_VAR_sbst")

! Local parameters:
! AR20: parType
! AR21: convCheck
! AR22: lastSS
! AR23: lastConvergedTime
! ar24: lastConvergedSubstep
! AR31: parmTime
! ar32: parmSubstep


! Prepare environment
pcbackup
nbackup
ebackup
mpopbackup
/uis,msgpop,3

! Parse inputs
AR31='LASTTIME_VAR_time'
*get,AR20,parm,arg1,type
*if,AR20,eq,3,then
  AR31=arg1
*endif

ar32 = 'LASTTIME_VAR_sbst'
*get,ar20,parm,ARG2,type
*if,ar20,eq,3,then
  ar32 = ARG2
*endif

! Read last substep of last load step
/post1
nsel,none
esel,none
subset,last

! Collect data to check convergence
*get , AR21 , active ,, solu , cnvg
*get , AR22 , active ,, set  , sbst

! Read previous substep if last one has not converged
*if,AR21,eq,0,or,AR22,eq,999999,then
  subset,prev  ! NOTE: 'prev' is an undocumented keyword for the SUBSET command, but seems to work as expected.
*endif

! Assign last TIME & SBST to output parameters
*get,AR23,active,,set,time
%AR31%=AR23

*get,ar24,active,,set,sbst
%ar32% = ar24

! Clean up
pcrestore
nrestore
elrestore
mpoprestore
