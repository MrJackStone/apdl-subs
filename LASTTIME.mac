! lasttime , outputTime , outputSbst
! LASTTIME , ARG1       , ARG2

! Description:
! Returns the TIME value of the last converged substep.

! Arguments:
! ARG1: outputTime  -  name of the output parameter to which the last TIME value will be assigned
!                      (defaults to "LASTTIME_VAR_time")
! ARG2: outputSbst  -  name of the output parameter to which the last SBST value will be assigned
!                      (defaults to "LASTTIME_VAR_sbst")

! Local parameters:
! ar20: parType
! ar21: convCheck
! ar22: lastSS
! ar23: lastConvergedTime
! ar24: lastConvergedSubstep
! ar31: parmTime
! ar32: parmSubstep


! Prepare environment
pcbackup
nbackup
ebackup
mpopbackup
/uis,msgpop,3

! Parse inputs
ar31 = 'LASTTIME_VAR_time'
*get,ar20,parm,ARG1,type
*if,ar20,eq,3,then
  ar31 = ARG1
*endif

ar32 = 'LASTTIME_VAR_sbst'
*get,ar20,parm,ARG2,type
*if,ar20,eq,3,then
  ar32 = ARG2
*endif

! Read last converged substep
/post1
nsel,none
esel,none
subset,last

*get , ar21 , active ,, solu , cnvg
*get , ar22 , active ,, set  , sbst

*if,ar21,eq,0,or,ar22,eq,999999,then
  subset,prev  ! NOTE: 'prev' is an undocumented keyword for the SUBSET command, but seems to work as expected.
*endif

! Assign last converged TIME & SBST to output parameters
*get,ar23,active,,set,time
%ar31% = ar23

*get,ar24,active,,set,sbst
%ar32% = ar24

! Clean up
pcrestore
nrestore
elrestore
mpoprestore
