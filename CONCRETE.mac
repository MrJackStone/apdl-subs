! concrete , matNum , fc   , dens , Poisson , ft   , linear , eRed
! CONCRETE , ARG1   , ARG2 , ARG3 , ARG4    , ARG5 , ARG6   , ARG7

! Arguments:
! ARG1: matNum   -  material number
! ARG2: fc       -  compressive strength
! ARG3: dens     -  concrete mass density (default: 2500)
! ARG4: Poisson  -  Poisson's coefficient (default: 0.3)
! ARG5: ft       -  tensile strength (default: 0.21*fck**(2/3))
! ARG6: linear   -  linear elastic key:
!                     [0]  OFF (nonlinear, Drucker-Prager)
!                     [1]  ON (linear elastic)
! ARG7: eRed     -  elastic modulus multiplier (for ARG6=1 only)

! Local parameters:
! ar20: parType
! ar21: massDensity
! ar22: Poisson
! ar23: eCU
! ar24: Oci
! ar25: Ocu
! ar26: Ocr
! ar27: Otr
! ar28: Gft
! ar29: fctm
! ar30: fct
! ar31: fcb
! ar32: Eti
! ar33: kcm
! ar34: kcu
! ar35: delt
! ar36: delc


! Standard values: mass density & Poisson's coefficient
*get,ar20,parm,ARG3,type
*if,ar20,ne,0,or,ARG3,le,0,then
  ar21 = 2500
*else
  ar21 = ARG3
*endif
*get,ar20,parm,ARG4,type
*if,ar20,ne,0,or,ARG4,le,0,then
  ar22 = 0.2
*else
  ar22 = ARG4
*endif

*get,ar20,parm,ARG6,type
*if,ar20,eq,0,and,ARG6,eq,1,then
  ! Linear elastic material

  ! Elastic modulus (NBR 6118)
  ar37 = 4760*sqrt(ARG2*1e-6)*1e6

  ! Elastic modulus reduction
  *get,ar20,parm,ARG7,type
  *if,ar20,eq,1,and,ARG7,gt,0,then
    ar37 = ar37*ARG7
  *endif

  ! Elastic properties
  mp , ex   , ARG1 , ar37
  mp , dens , ARG1 , ar21
  mp , prxy , ARG1 , ar22

*else
  ! Nonlinear model: Drucker-Prager

  ! Tensile strength
  *get,ar20,parm,ARG5,type
  *if,ar20,ne,0,or,ARG5,le,0,then
    ar29 = 0.3*ARG2**(2/3)
    ar30 = 0.7*ar29
  *else
    ar30 = ARG5
  *endif

  ! Deformation at peak compressive stress
  ar23 = 2.5e-3

  ! Exponential HSD constants:
  ! Relative stress at start of nonlinear hardening
  ar24 = 0.33
  ! Residual relative stress at kcu
  ar25 = 0.85
  ! Residual compressive relative stress
  ar26 = 0.20
  ! Residual tensile relative stress
  ar27 = 0.1
  ! Mode I area-specific fracture energy (units: Force/Length)
  ar28 = 100

  ! Dilatancy constants:
  ! Dilatancy factor in tension
  ar35 = 0.25
  ! Dilatancy factor in compression
  ar36 = 1.00

  ! Biaxial compressive resistance
  ar31 = 1.2*ARG2

  ! Tangent initial elastic modulus
  ar32 = 1.5*ARG2/ar23
  ! Plastic strain at uniaxial compressive strength
  ar33 = ar23-(ARG2/ar32)
  ! Plastic strain at transition from power law to exponential softening
  ar34 = 1.75*ar23-ar25*(ARG2/ar32)

  ! Elastic properties
  mp , ex   , ARG1 , ar32
  mp , prxy , ARG1 , ar22
  mp , dens , ARG1 , ar21

  ! Drucker-Prager
  tb     , conc , ARG1 ,      ,      , dp
  tbdata , 1    , ARG2 , ar30 , ar31

  ! Dilatancy
  tb     , conc , ARG1 ,      ,  , dila
  tbdata , ARG1 , ar35 , ar36

  ! Exponential hardening/softening/dilatation (HSD2)
  tb     , conc , ARG1 ,      ,      , hsd2
  tbdata , 1    , ar33 , ar34 , ar24 , ar25 , ar26
  tbdata , 6    , ar28 , ar27

*endif
