! concrlaw , type , fck  , E    , pointNumber
! CONCRLAW , ARG1 , ARG2 , ARG3 , ARG4

! Description:
! Creates arrays with [sigma ; epsilon] values corresponding to one of the following concrete
! stress-strain curves:
!   [0]  international literature curve (without tail)
!   [1]  international literature curve (with tail)
!   [2]  ABNT NBR 6118:2014's curve (without tail)
!   [3]  ABNT NBR 6118:2014's curve (with tail)
!
! Curve types 0 & 1 (international literature) have two or three segments (linear-ascending,
! nonlinear, and an optional linear-flat segment) and are defined by three or four points:
!   1  [0  ; 0 ]  origin
!   2  [el ; fl]  linear/nonlinear threshold
!   3  [ey ; fc]  end of nonlinear segment
!   4  [eu ; fc]  end of flat tail (for type 1 only)
! The required parameters (el, ey, eu, & fl) are defined as follows:
!   fl = 0.3 * fc
!   el = fl / E
!   ey = 2 * (fc / E)
!   eu = 2 * ey
!
! Curve types 2 & 3 (ABNT NBR 6118:2014) have one or two segments (nonlinear-ascending, and
! an optional linear-flat segment) and are defined by two or three points:
!   1  [0  ; 0 ]  origin
!   3  [ey ; fc]  end of nonlinear segment
!   4  [eu ; fc]  end of flat tail (type 3 only)
! The required parameters (ey, eu, n) are defined as follows:
!   n  = min ( 2 ; 1.4 + 23.4 * ((90 - fc)/1000)^4)
!
!         | if fc<=50 : 0.002
!   ey = <|
!         | if fc>50  : 0.002 + 0.00085 * (fc - 50)^0.53
!
!         | if fc<=50 : 0.0035
!   eu = <|
!         | if fc>50  : 0.0026 + 0.0035 * ((90 - fc)/100)^4

! Arguments:
! ARG1: type  -  shape of stress-strain curve, as defined above (0, 1, 2, or 3)
! ARG2: fck   -  concrete's compressive strength
! ARG3: E     -  concrete's initial elastic modulus (for type 0 and type 1 only)
! ARG4: divs  -  number of points used to discretize the nonlinear segment of the stress-strain
!                curves (defaults to 5)

! Local parameters:
! ar20: parType
! ar21: tail
! ar22: corrPtNumber
! ar23: epsilonC2
! ar24: epsilonC1
! ar25: fc1
! ar26: deltaEpsilon
! ar27: nthPoint
! ar28: nthEpsilon
! ar29: nthSigma
! ar30: epsilonCU
! ar31: n
! ar43: initialElasticModulus
! ar44: nonlinearDivs

! Output parameters:
! CONCRLAW_ARR_eVector
! CONCRLAW_ARR_sVector
! CONCRLAW_VAR_E0


! Parse inputs
ar44 = 5
*get,ar20,parm,ARG4,type
*if,ar20,eq,0,then
  *if,ARG4,gt,0,then
    ar44 = ARG4
  *endif
*endif

ar21 = 0
*if,ARG1,eq,1,or,ARG1,eq,3,then
  ar21 = 1
*endif

ar43 = 5600*sqrt(ARG2/1e6)*1e6
*get,ar20,parm,ARG3,type
*if,ar20,eq,0,then
  *if,ARG3,ne,0,then
    ar43 = ARG3
  *endif
*endif

! Prepare environment
ar22 = ar44
*if,ar21,eq,1,then
  ar22 = ar22+1
*endif

*del , CONCRLAW_ARR_eVector ,  , nopr
*del , CONCRLAW_ARR_sVector ,  , nopr
*del , CONCRLAW_VAR_E0      ,  , nopr
*dim , CONCRLAW_ARR_eVector ,  , ar22
*dim , CONCRLAW_ARR_sVector ,  , ar22

! Calculate stress-strain points
*if,ARG1,eq,0,or,ARG1,eq,1,then
  ! Cuve shape: international literature

  ! Calculate curve parameters:
  ! fl
  ar25 = 0.3*ARG2
  ! ey
  ar23 = 2*ARG2/ar43
  ! el
  ar24 = ar25/ar43
  ! eu
  ar30 = 2*ar23
  ! de
  ar26 = (ar23-ar24)/(ar44-1)

  ! Calculate stress-strain values for nonlinear segment
  CONCRLAW_ARR_eVector(1)    = ar24
  CONCRLAW_ARR_sVector(1)    = ar25
  CONCRLAW_ARR_eVector(ar44) = ar23
  CONCRLAW_ARR_sVector(ar44) = ARG2
  *do,ar27,2,ar44-1,1
    ar28                       = ((ar27-1)*ar26)+ar24
    ar29                       = (ar43*ar28)/(1+((ar28/ar23)**2))
    CONCRLAW_ARR_eVector(ar27) = ar28
    CONCRLAW_ARR_sVector(ar27) = ar29
  *enddo

  ! Calculate stress-strain point at the end of the tail
  *if,ar21,eq,1,then
    CONCRLAW_ARR_eVector(ar22) = ar30
    CONCRLAW_ARR_sVector(ar22) = CONCRLAW_ARR_sVector(ar22-1)
  *endif

  ! Output initial elastic modulus
  CONCRLAW_VAR_E0 = ar43

*elseif,ARG1,EQ,2,OR,ARG1,EQ,3,THEN
  ! Curve shape: ABNT NBR 6118:2014

  ! Calculate curve parameters:
  ! ey
  ar23 = 2/1000
  *if,ARG2,gt,50e6,then
    ar23 = ar23+(0.085/1000)*((ARG2/1e6)-50)**(0.53)
  *endif
  ! eu
  ar30 = 3.5/1000
  *if,ARG2,gt,50e6,then
    ar30 = (2.6/1000)+(35/1000)*((90-(ARG2/1e6))/100)**4
  *endif
  ! de
  ar26 = ar23/ar44
  ! n
  ar31 = (2<1.4+23.4*((90-(ARG2/1e6))/100)**4)

  ! Calculate stress-strain values for nonlinear segment
  *do,ar27,1,ar44,1
    ar28                       = ar27*ar26
    ar29                       = ARG2*(1-((1-(ar28/ar23))**ar31))
    CONCRLAW_ARR_eVector(ar27) = ar28
    CONCRLAW_ARR_sVector(ar27) = ar29
  *enddo

  ! Calculate stress-strain point at the end of the tail
  *if,ar21,eq,1,then
    CONCRLAW_ARR_eVector(ar22) = ar30
    CONCRLAW_ARR_sVector(ar22) = CONCRLAW_ARR_sVector(ar22-1)
  *endif

  ! Output initial elastic modulus
  CONCRLAW_VAR_E0 = CONCRLAW_ARR_sVector(1)/CONCRLAW_ARR_eVector(1)

*endif
