! DEFETABS, elemType, prefix, scale, freeze
! DEFETABS,     arg1,   arg2,  arg3,   arg4

! Arguments:
! arg1: elemType  -  number identifying element type: 180, 181, or 188
! arg2: prefix    -  element table name prefix (max 5 characters)
! arg3: scale     -  element table scale factor (usually 1e-3, to convert to kN)
! arg4: freeze    -  determines if the created element tables will be of type CALC,
!                    being therefore frozen (not updating) to ETAB,REFL:
!                        [0] Not frozen (updates with REFL)
!                        [1] Frozen (does not update with REFL)

! Local parameters:
! AR20: et_count
! AR21: parType
! AR22: strPrefix
! AR23: et_lab
! AR24: et_num
! AR25: nthET


*if,arg1,eq,188,then
    ! BEAM188
    AR20=12
    *del,DEFETABS_ARR_names
    *del,DEFETABS_ARR_numbers
    *dim,DEFETABS_ARR_names,char,AR20
    *dim,DEFETABS_ARR_numbers,,AR20
    DEFETABS_ARR_names(1)   = 'Ni' , 'Nj' , 'QZi' , 'QZj' , 'QYi' , 'QYj' , 'MYi' , 'MYj' , 'MZi' , 'MZj' , 'TQi' , 'TQj'
    DEFETABS_ARR_numbers(1) = 1    , 14   , 5     , 18    , 6     , 19    , 2     , 15    , 3     , 16    , 4     , 17

*elseif,arg1,eq,181,then
    ! SHELL181
    AR20=6
    *del,DEFETABS_ARR_names
    *del,DEFETABS_ARR_numbers
    *dim,DEFETABS_ARR_names,char,AR20
    *dim,DEFETABS_ARR_numbers,,AR20
    DEFETABS_ARR_names(1)  ='MX','QX','NX','MY','QY','NY'
    DEFETABS_ARR_numbers(1)=  5 ,  7 ,  1 ,  4 ,  8 ,  2

*elseif,arg1,eq,180,then
    ! LINK180

*endif

*get,AR21,parm,arg2,type
*if,AR21,eq,3,then
    AR22=arg2
*else
    AR22=''
*endif


*do,AR25,1,AR20,1
    AR23=strcat(AR22,DEFETABS_ARR_names(AR25))
    AR24=DEFETABS_ARR_numbers(AR25)
    etable,AR23,smisc,AR24
    *if,arg3,ne,1,and,arg3,ne,0,then
        sadd,AR23,AR23,,arg3
    *endif

    *if,arg4,eq,1,then
        sadd,AR23,AR23,,1
    *endif
*enddo

