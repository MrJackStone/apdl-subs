! TAKEPIC, FileName, FileSize, VectorMode, graphMode, fileExt, folder
! TAKEPIC, arg1,     arg2,     arg3,       arg4,      arg5,    arg6

! Description:
! Captures the image currently displayed on the screen. Resolution, file name, vector display
! and file format can be set using the arguments.

! Arguments:
! arg1: FileName    -  name of the output file
! arg2: FileSize    -  image resolution (default = 2400)
! arg3: VectorMode  -  vector display (wireframe):
!                         [0] No change;
!                         [1] Force wireframe;
!                         [2] Force raster;
! arg4: graphMode   -  graphical display type (command /graphics):
!                         [0] No change;
!                         [1] FULL;
!                         [2] PowerGraphics;
! arg5: fileExt     -  image file format
!                         [0] PNG
!                         [1] JPG
! arg6: folder      -  name of the folder in which the file will be saved (optional).
!                      If a folder name is given, the folder will be created inside the current
!                      working directory. Defaults to the current working directory.

! Local parameters:
! AR20: ArgType
! AR21: WireframeOn
! AR22: ParmType
! AR23: imgExt
! AR24: outputFileName
! AR25: folderPrefix
! AR26: renameOpt
! AR27: newName

! Global parameters (erased at the end of this subroutine):
! TAKEPIC_filename


! Set rename file option
AR26=0
AR27=''
*get,AR20,parm,arg1,type
*if,AR20,eq,3,then
  AR26=1
  AR27=arg1
*endif

! Define output file's name, prefixed with the optional folder name
AR25=''
*get,AR20,parm,arg6,type
*if,AR20,eq,3,then
  /syp,mkdir,arg6
  AR25=strcat(arg6,'\')
*endif
AR24=strcat(AR25,AR27)

! Image file format
AR23='PNG'
*get,AR22,parm,arg5,type
*if,AR22,eq,0,then
   *if,arg5,eq,1,then
      AR23='JPG'
   *endif
*endif

! Graphical display definition
*get,AR20,parm,arg4,type
*if,AR20,eq,0,then
   *if,arg4,eq,1,then
      /graphics,full
   *elseif,arg4,eq,2,then
      /graphics,power
   *endif
*endif

! Vector display definition
*get,AR21,graph,,display
*get,AR22,parm,arg3,type
*if,AR22,eq,0,then
   *if,arg3,eq,1,then
      AR21=1
   *elseif,arg3,eq,2,then
      AR21=0
   *endif
*endif

! Change graphical output
*if,AR23,eq,'PNG',then
   /show,png,,AR21
*elseif,AR23,eq,'JPG',then
   /show,jpeg,,AR21
*endif

! Basic settings
pngr,comp,on,-1
pngr,orient,horiz
pngr,color,2
pngr,tmod,1

! Image resolution
*if,arg2,gt,0,then
   /gfile,arg2
*else
   /gfile,2400
*endif

! Create temporary color map
/cmap,_TEMPCMAP_,cmp,,save
/rgb,index,100,100,100, 0
/rgb,index, 80, 80, 80,13
/rgb,index, 60, 60, 60,14
/rgb,index, 0, 0, 0,15

! Draw image (creates the file)
/replot

! Delete temporary color map
/cmap,_TEMPCMAP_,cmp
/delete,_TEMPCMAP_,cmp

! Reset graphical output
/show,close,,AR21
/rgb,index, 0, 0, 0, 0
/rgb,index, 60, 60, 60,13
/rgb,index, 80, 80, 80,14
/rgb,index,100,100,100,15

! Rename file
*if,AR26,eq,1,then
   *del,TAKEPIC_filename
   *get,TAKEPIC_filename,active,0,jobnam
   /rename,'%TAKEPIC_filename%000','%AR23%',,'%AR24%','%AR23%'
*endif

! Clean up
*del,TAKEPIC_filename,,nopr
