! VCAST,  L,     bf,    dm,    d0,    sw,    s0,    se,    theta,  bos,   x0,    y0,    z0,    swi,   swf
! VCAST,  arg1,  arg2,  arg3,  arg4,  arg5,  arg6,  arg7,  arg8,   arg9,  ar10,  ar11,  ar12,  ar13,  ar14

! Descrição:
! Criar solid model de uma viga de seção transversal tipo perfil I duplamente simétrico
! com aberturas hexagonais na alma (viga castelada). As aberturas podem ou não ter
! enrijecedores e as larguras dos montantes inicial e final podem ser definidas diretamente.

! Lista de variáveis locais:
! AR20: Stiffener
! AR21: sb
! AR22: LMod
! AR23: CustomP0
! AR24: q1
! AR25: AQuant
! AR26: Rem
! AR27: P0
! AR28: COMPK_NEW
! AR29: COMPL_NEW
! AR30: COMPA_NEW
! AR31: MaxKP
! AR32: MaxLN
! AR33: HexArea
! AR34: HexAreas
! AR36: WebArea
! AR73: OutsideXMin
! AR74: OutsideXMax
! AR77: OutsideZMin
! AR78: OutsideZMax
! AR80: COMPL_OpBorders
! AR81: MaxKP2
! AR82: DragLine1
! AR83: DragLine2
! AR84: ArgType
! AR85: LCount
! AR86: COMPL_DragLines
! AR90: LNum

! Lista de variáveis globais (apagadas ao final da subrotina):
! VCAST_ARR_LNUMS
! VCAST_ARR_KPNUMS

! Lista de variáveis de saída:
!    COMPONENTES:
!       VCAST_COMPA_ALMA
!          Componente contendo todas as áreas da alma da peça
!       VCAST_COMPA_MESAS
!          Componente contendo todas as áreas das mesas da peça
!       VCAST_COMPA_ENRIJECEDORES
!          Componente contendo todas as áreas dos enrijecedores
!          das aberturas (se houver algum)
!    GEOMETRIA:
!       VCAST_VAR_L
!       VCAST_VAR_bf
!       VCAST_VAR_dm
!       VCAST_VAR_d0
!       VCAST_VAR_sw
!       VCAST_VAR_s0
!       VCAST_VAR_se
!       VCAST_VAR_sb
!       VCAST_VAR_theta
!       VCAST_VAR_bos
!       VCAST_VAR_x0
!       VCAST_VAR_y0
!       VCAST_VAR_z0
!       VCAST_VAR_n
!       VCAST_VAR_swi
!       VCAST_VAR_swf

/prep7


!!                                                                                                                                            !!
!! 1.   DADOS CALCULADOS                                                                                                                      !!
!!                                                                                                                                            !!

! Verificar tipo de definição de geometria da abertura
*afun,DEG
*if,arg4,GT,0,THEN
   *if,arg5,GT,0,AND,arg6,GT,0,THEN
      arg7=arg6+arg5
      arg8=ATAN(arg4/(arg6-arg5))
   *elseif,arg7,GT,0,AND,arg8,GT,0,THEN
      arg5=(arg7/2)-(arg4/(2*TAN(arg8)))
      arg6=(arg4/TAN(arg8))+arg5
   *elseif,arg8,GT,0,THEN
      arg5=(arg4/2)/SIN(arg8)
      arg6=2*((arg4/2)/TAN(arg8))+arg5
      arg7=arg6+arg5
   *else
      *msg,ERROR,0
      VCAST ERROR: INVALID PARAMETERS (%I).
   *endif
*else
   *msg,ERROR,1
   VCAST ERROR: INVALID PARAMETERS (%I).
*endif
*afun,RAD

*if,arg9,GT,0,THEN
   AR20=1
*else
   AR20=0
*endif
! Projeção horizontal (em x) da face inclinada
AR21=(arg6-arg5)/2
! Calcular comprimento modificado por largura de montante inicial
*if,ar13,GT,0,THEN
   *if,ar13,LT,arg1,THEN
      AR22=arg1-ar13
      AR23=1
   *else
      *msg,ERROR,2
      VCAST ERROR: INVALID W0 (%I).
   *endif
*else
   AR22=arg1
   AR23=0
*endif
! Calcular comprimento modificado por largura de montante final
*if,ar14,GT,0,THEN
   *if,ar14,LT,arg1,THEN
      AR22=AR22-ar14
      AR23=AR23+2
   *else
      *msg,ERROR,3
      VCAST ERROR: INVALID WF (%I).
   *endif
*endif
! Corrigir o comprimento de distribuição modificado
*if,AR23,EQ,1,THEN
   AR22=AR22+(arg5/2)
*elseif,AR23,EQ,2,THEN
   AR22=AR22+(arg5/2)
*elseif,AR23,EQ,3,THEN
   AR22=AR22+arg5
*endif
! Calcular quantidade de aberturas a ser criadas
AR24=AR22/arg7
AR25=NINT(AR24)
*if,AR25,GT,AR24,THEN
   AR25=AR25-1
*endif
!   Posição (em x) do eixo da primeira abertura
!     (mais próxima à face 1)
*if,AR23,EQ,0,THEN
   AR26=AR24-AR25
   AR27=((AR26*arg7)/2)+AR21+arg5
*elseif,AR23,EQ,1,THEN
   AR27=ar13+(arg6/2)
*elseif,AR23,EQ,2,THEN
   AR27=arg1-ar14-(AR25*arg7)+arg5+(arg6/2)
*elseif,AR23,EQ,3,THEN
   AR27=ar13+(arg6/2)
   arg1=AR27+AR25*arg7-(arg6/2)-arg5+ar14
   *msg,WARN,arg1
   COMPRIMENTO TOTAL ALTERADO PARA %10.4F.
*endif


!!                                                                                                                                            !!
!! 2.   SOLID MODEL                                                                                                                           !!
!!                                                                                                                                            !!

SELTOL,1e-8

! Desativar exibição de mensagens de aviso
/uis,MSGPOP,3

! Fazer backup das entidades do solid model atualmente selecionadas
KBACKUP
LBACKUP
ABACKUP

! Remover keypoints, linhas e áreas existentes da seleção
KSEL,U,KP,,ALL
LSEL,U,LINE,,ALL
ASEL,U,AREA,,ALL

! Criar componentes de keypoints, linhas e áreas novas
CM,AR28,KP
CM,AR29,LINE
CM,AR30,AREA

*get,AR31,KP,0,NUM,MAXD

*del,VCAST_ARR_KPNUMS
*dim,VCAST_ARR_KPNUMS,ARRAY,18
*vfill,VCAST_ARR_KPNUMS,RAMP,AR31+1,1
NUMSTR,KP,AR31+1

! Keypoints da seção transversal (face 1)
K,VCAST_ARR_KPNUMS(1), ar10, ar11,      ar12-arg2/2
K,VCAST_ARR_KPNUMS(2), ar10, ar11,      ar12
K,VCAST_ARR_KPNUMS(3), ar10, ar11,      ar12+arg2/2
K,VCAST_ARR_KPNUMS(4), ar10, ar11+arg3, ar12-arg2/2
K,VCAST_ARR_KPNUMS(5), ar10, ar11+arg3, ar12
K,VCAST_ARR_KPNUMS(6), ar10, ar11+arg3, ar12+arg2/2

! Keypoints da seção transversal (face 2)
K,VCAST_ARR_KPNUMS(7),  ar10+arg1, ar11,      ar12-arg2/2
K,VCAST_ARR_KPNUMS(8),  ar10+arg1, ar11,      ar12
K,VCAST_ARR_KPNUMS(9),  ar10+arg1, ar11,      ar12+arg2/2
K,VCAST_ARR_KPNUMS(10), ar10+arg1, ar11+arg3, ar12-arg2/2
K,VCAST_ARR_KPNUMS(11), ar10+arg1, ar11+arg3, ar12
K,VCAST_ARR_KPNUMS(12), ar10+arg1, ar11+arg3, ar12+arg2/2

CM,AR28,KP

*get,AR32,LINE,0,NUM,MAXD

*del,VCAST_ARR_LNUMS
*dim,VCAST_ARR_LNUMS,ARRAY,22
*vfill,VCAST_ARR_LNUMS,RAMP,AR32+1,1
NUMSTR,LINE,AR32+1

! Linhas da seção transversal (face 1)
L, VCAST_ARR_KPNUMS(1), VCAST_ARR_KPNUMS(2)
L, VCAST_ARR_KPNUMS(2), VCAST_ARR_KPNUMS(3)
L, VCAST_ARR_KPNUMS(4), VCAST_ARR_KPNUMS(5)
L, VCAST_ARR_KPNUMS(5), VCAST_ARR_KPNUMS(6)
L, VCAST_ARR_KPNUMS(2), VCAST_ARR_KPNUMS(5)

! Linhas da seção transversal (face 2)
L, VCAST_ARR_KPNUMS(7),  VCAST_ARR_KPNUMS(8)
L, VCAST_ARR_KPNUMS(8),  VCAST_ARR_KPNUMS(9)
L, VCAST_ARR_KPNUMS(10), VCAST_ARR_KPNUMS(11)
L, VCAST_ARR_KPNUMS(11), VCAST_ARR_KPNUMS(12)
L, VCAST_ARR_KPNUMS(8),  VCAST_ARR_KPNUMS(11)

! Linhas longitudinais
L, VCAST_ARR_KPNUMS(3), VCAST_ARR_KPNUMS(9)
L, VCAST_ARR_KPNUMS(2), VCAST_ARR_KPNUMS(8)
L, VCAST_ARR_KPNUMS(1), VCAST_ARR_KPNUMS(7)
L, VCAST_ARR_KPNUMS(6), VCAST_ARR_KPNUMS(12)
L, VCAST_ARR_KPNUMS(5), VCAST_ARR_KPNUMS(11)
L, VCAST_ARR_KPNUMS(4), VCAST_ARR_KPNUMS(10)

CM,AR29,LINE

! Áreas das mesas e alma do perfil
AL, VCAST_ARR_LNUMS(2),  VCAST_ARR_LNUMS(11), VCAST_ARR_LNUMS(7),  VCAST_ARR_LNUMS(12)
AL, VCAST_ARR_LNUMS(1),  VCAST_ARR_LNUMS(12), VCAST_ARR_LNUMS(6),  VCAST_ARR_LNUMS(13)
AL, VCAST_ARR_LNUMS(4),  VCAST_ARR_LNUMS(14), VCAST_ARR_LNUMS(9),  VCAST_ARR_LNUMS(15)
AL, VCAST_ARR_LNUMS(3),  VCAST_ARR_LNUMS(15), VCAST_ARR_LNUMS(8),  VCAST_ARR_LNUMS(16)
AL, VCAST_ARR_LNUMS(12), VCAST_ARR_LNUMS(10), VCAST_ARR_LNUMS(15), VCAST_ARR_LNUMS(5)

CM,AR30,AREA

! Caso haja ao menos uma abertura:
*if,AR25,GT,0,THEN

   ! Keypoints da primeira abertura hexagonal
   K,VCAST_ARR_KPNUMS(13), ar10+AR27-(arg6/2), ar11+arg3/2,        ar12
   K,VCAST_ARR_KPNUMS(14), ar10+AR27-(arg5/2), ar11+(arg3-arg4)/2, ar12
   K,VCAST_ARR_KPNUMS(15), ar10+AR27+(arg5/2), ar11+(arg3-arg4)/2, ar12
   K,VCAST_ARR_KPNUMS(16), ar10+AR27+(arg6/2), ar11+arg3/2,        ar12
   K,VCAST_ARR_KPNUMS(17), ar10+AR27+(arg5/2), ar11+(arg3+arg4)/2, ar12
   K,VCAST_ARR_KPNUMS(18), ar10+AR27-(arg5/2), ar11+(arg3+arg4)/2, ar12

   CM,AR28,KP

   ! Linhas da primeira abertura hexagonal
   L, VCAST_ARR_KPNUMS(13), VCAST_ARR_KPNUMS(14)
   L, VCAST_ARR_KPNUMS(14), VCAST_ARR_KPNUMS(15)
   L, VCAST_ARR_KPNUMS(15), VCAST_ARR_KPNUMS(16)
   L, VCAST_ARR_KPNUMS(16), VCAST_ARR_KPNUMS(17)
   L, VCAST_ARR_KPNUMS(17), VCAST_ARR_KPNUMS(18)
   L, VCAST_ARR_KPNUMS(18), VCAST_ARR_KPNUMS(13)

   CM,AR29,LINE

   ! Área da primeira abertura hexagonal
   ASEL,U,AREA,,ALL
   AL,VCAST_ARR_LNUMS(17),VCAST_ARR_LNUMS(18),VCAST_ARR_LNUMS(19),VCAST_ARR_LNUMS(20),VCAST_ARR_LNUMS(21),VCAST_ARR_LNUMS(22)
   *get,AR33,AREA,0,NUM,MIN

   ! Vértices do retângulo externo de corte
   AR73=ar10-arg1
   AR74=ar10+2*arg1
   AR77=ar12-arg2
   AR78=ar12+arg2

   ! Copiar área da abertura hexagonal (AQUANT vezes)
   AGEN,AR25,AR33,,,arg7
   CM,AR34,AREA
   CMSEL,A,AR30
   CM,AR30,AREA

   ! Obter o número da área que representa a alma do perfil
   CMSEL,U,AR34
   ASEL,R,LOC,Z,ar12
   *get,AR36,AREA,0,NUM,MIN
   CMSEL,A,AR30
   CM,AR30,AREA
   CMSEL,A,AR34

   ! Cortar aberturas hexagonais
   ASBA,AR36,AR34,,,DELETE,DELETE
   CMSEL,A,AR30
   CM,AR30,AREA
   CMSEL,A,AR29
   CM,AR29,LINE
   CMSEL,A,AR28
   CM,AR28,KP

   ! Fatiar horizontalmente áreas no meio da altura
   CMSEL,S,AR28
   CMSEL,A,AR29
   CMSEL,A,AR30
   SLICEXZ,ar11+arg3/2,AR73,AR77,AR74,AR78
   CM,AR28,KP
   CM,AR29,LINE
   CM,AR30,AREA

   ! OBS.: Opção de fatiar nos demais vértices dos hexágonos (horizontal
   !       e verticalmente) excluída por gerar malha desfavorável.

   ! Criar enrijecedores nas aberturas
   *if,AR20,EQ,1,THEN

       ! Selecionar apenas linhas das aberturas
       LSEL,S,LOC,Z,ar12
       LSEL,U,LOC,Y,ar11+arg3/2
       LSEL,U,LOC,Y,ar11
       LSEL,U,LOC,Y,ar11+arg3
       LSEL,U,LOC,X,ar10
       LSEL,U,LOC,X,ar10+arg1
       CM,AR80,LINE
       *get,AR85,LINE,,COUNT

        ! Criar linhas de direção de arrasto
        *get,AR81,KP,,NUM,MAXD
        K,AR81+1,ar10-arg1,ar11,ar12
        K,AR81+2,ar10-arg1,ar11,ar12-arg9
        K,AR81+3,ar10-arg1,ar11,ar12+arg9
        LSEL,U,LINE,,ALL
        L,AR81+1,AR81+2
        *get,AR82,LINE,,NUM,MAX
        L,AR81+1,AR81+3
        *get,AR83,LINE,,NUM,MAX
        CM,AR86,LINE

        ! Criar áreas dos enrijecedores de abertura
        AR90=0
        *do,AR86,1,AR85,1
            CMSEL,S,AR80
            AR90=LSNEXT(AR90)
            CMSEL,A,AR86
            ADRAG,AR90,,,,,,AR82
            ADRAG,AR90,,,,,,AR83
        *enddo
        LSLA,A
        KSLL,A

        ! Excluir entidades de arrasto
        LDELE,AR82
        LDELE,AR83
        KDELE,AR81+1
        KDELE,AR81+2
        KDELE,AR81+3

        ! Mesclar entidades
        NUMMRG,KP

        ! Atualizar componentes
        CMSEL,A,AR28
        CM,AR28,KP
        CMSEL,A,AR29
        CM,AR29,LINE
        CMSEL,A,AR30
        CM,AR30,AREA

   *endif

*endif


!!                                                                                                                                            !!
!! 3.   SAÍDA                                                                                                                                 !!
!!                                                                                                                                            !!

! Definir componente de saída das mesas
ASEL,S,LOC,Y,ar11
ASEL,A,LOC,Y,ar11+arg3
CM,VCAST_COMPA_MESAS,AREA

! Definir componente de saída da alma
ASEL,INVE
ASEL,R,LOC,Z,ar12
CM,VCAST_COMPA_ALMA,AREA

! Definir componente de saída dos enrijecedores
*if,AR20,EQ,1,THEN
   CMSEL,S,AR30
   ASEL,U,LOC,Y,ar11+arg3
   ASEL,U,LOC,Y,ar11
   ASEL,U,LOC,Z,ar12
   CM,VCAST_COMPA_ENRIJECEDORES,AREA
*endif

! Restaurar seleção prévia
KRESTORE
LRESTORE
ARESTORE

! Selecionar novas entidades criadas
CMSEL,A,VCAST_COMPA_ALMA
CMSEL,A,VCAST_COMPA_MESAS
CMSEL,A,AR28
CMSEL,A,AR29
CMSEL,A,AR30

! Excluir componentes
CMDELE,AR28
CMDELE,AR29
CMDELE,AR30

*if,AR20,EQ,1,THEN
   CMSEL,A,VCAST_COMPA_ENRIJECEDORES
*endif

*del,VCAST_ARR_LNUMS
*del,VCAST_ARR_KPNUMS

*get,AR84,PARM,arg1,TYPE
*if,AR84,EQ,-1,THEN
   VCAST_VAR_L=0
*else
   VCAST_VAR_L=arg1
*endif
*get,AR84,PARM,arg2,TYPE
*if,AR84,EQ,-1,THEN
   VCAST_VAR_bf=0
*else
   VCAST_VAR_bf=arg2
*endif
*get,AR84,PARM,arg3,TYPE
*if,AR84,EQ,-1,THEN
   VCAST_VAR_dm=0
*else
   VCAST_VAR_dm=arg3
*endif
*get,AR84,PARM,arg4,TYPE
*if,AR84,EQ,-1,THEN
   VCAST_VAR_d0=0
*else
   VCAST_VAR_d0=arg4
*endif
*get,AR84,PARM,arg5,TYPE
*if,AR84,EQ,-1,THEN
   VCAST_VAR_sw=0
*else
   VCAST_VAR_sw=arg5
*endif
*get,AR84,PARM,arg6,TYPE
*if,AR84,EQ,-1,THEN
   VCAST_VAR_s0=0
*else
   VCAST_VAR_s0=arg6
*endif
*get,AR84,PARM,arg7,TYPE
*if,AR84,EQ,-1,THEN
   VCAST_VAR_se=0
*else
   VCAST_VAR_se=arg7
*endif
*get,AR84,PARM,AR21,TYPE
*if,AR84,EQ,-1,THEN
   VCAST_VAR_sb=0
*else
   VCAST_VAR_sb=AR21
*endif
*get,AR84,PARM,arg8,TYPE
*if,AR84,EQ,-1,THEN
   VCAST_VAR_theta=0
*else
   VCAST_VAR_theta=arg8
*endif
*get,AR84,PARM,arg9,TYPE
*if,AR84,EQ,-1,THEN
   VCAST_VAR_bos=0
*else
   VCAST_VAR_bos=arg9
*endif
*get,AR84,PARM,ar10,TYPE
*if,AR84,EQ,-1,THEN
   VCAST_VAR_x0=0
*else
   VCAST_VAR_x0=ar10
*endif
*get,AR84,PARM,ar11,TYPE
*if,AR84,EQ,-1,THEN
   VCAST_VAR_y0=0
*else
   VCAST_VAR_y0=ar11
*endif
*get,AR84,PARM,ar12,TYPE
*if,AR84,EQ,-1,THEN
   VCAST_VAR_z0=0
*else
   VCAST_VAR_z0=ar12
*endif
*get,AR84,PARM,AR25,TYPE
*if,AR84,EQ,-1,THEN
   VCAST_VAR_n=0
*else
   VCAST_VAR_n=AR25
*endif
VCAST_VAR_swi=AR27-arg6/2
VCAST_VAR_swf=arg1-(AR25*arg7-arg5+VCAST_VAR_swi)



