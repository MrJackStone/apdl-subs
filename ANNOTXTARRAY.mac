! ANNOTXTARRAY, text_strings,   x0,   y0, spacing, direction, cellWidth, alignment
! ANNOTXTARRAY,         ARG1, ARG2, ARG3,    ARG4,      ARG5,      ARG6,      ARG7

! Description:
! Creates an array (along horizontal or vertical direction) of annotations.

! Arguments:
! ARG1: text_strings  -  name of array or chars with text to be written
! ARG2: x0            -  X coordinate (in window display units) of first text annotation
! ARG3: y0            -  Y coordinate (in window display units) of first text annotation
! ARG4: spacing       -  distance between annotations
! ARG5: direction     -  direction along which the array of annotations is distributed:
!                           ['X'] annotations distributed along a horizontal line
!                           ['Y'] annotations distributed along a vertical line
! ARG6: cellWidth     -  width of the virtual cell in which the annotation will be aligned
!                        (required only if ARG7 > 0)
! ARG7: alignment     -  text alignment inside cell:
!                           [0] left-justified
!                           [1] centered
!                           [2] right-justified

! Local parameters:
! ar21: cellCount
! ar22: nthCell
! ar23: textValue
! ar24: charCount
! ar25: calculatedWidth
! ar26: widthDifference
! ar27: leftPadding
! ar32: xCoord
! ar33: yCoord

! Global parameters (erased at the end of this subroutine):
! ANNOTXTARRAY_ARR_X
! ANNOTXTARRAY_ARR_Y
! ANNOTXTARRAY_VAR_charWidth


! Get number of text annotations
*get,ar21,parm,%ARG1%,dim,y

! Calculate X-Y coordinates of each annotation
*del,ANNOTXTARRAY_ARR_X,,nopr
*dim,ANNOTXTARRAY_ARR_X,,ar21
*del,ANNOTXTARRAY_ARR_Y,,nopr
*dim,ANNOTXTARRAY_ARR_Y,,ar21
*if,upcase(ARG5),eq,'X',then
  *vfill,ANNOTXTARRAY_ARR_X,ramp,ARG2,ARG4
  *vfill,ANNOTXTARRAY_ARR_Y,ramp,ARG3,0
*elseif,upcase(ARG5),eq,'Y',then
  *vfill,ANNOTXTARRAY_ARR_X,ramp,ARG2,0
  *vfill,ANNOTXTARRAY_ARR_Y,ramp,ARG3,ARG4
*else
  *msg,warn
ANNOTXTARRAY error: invalid direction (ARG5).%/&
ANNOTXTARRAY command is ignored.
  *return,-1
*endif

! Get character width
g_annocharwidth,'ANNOTXTARRAY_VAR_charWidth'

! Create array of text annotations
*do,ar22,1,ar21,1

  ! Text content !(from last to first)
  !ar23=%ARG1%(1,ar21-ar22+1)
  ar23=%ARG1%(1,ar22)

  ! Calculate difference between text width and cell width
  ar24=strleng(ar23)
  ar25=ar24*ANNOTXTARRAY_VAR_charWidth
  ar26=ARG6-ar25

  ! Calculate padding to align text
  *if,ARG7,eq,1,then
    ! Centered
    ar27=ar26/2
  *elseif,ARG7,eq,2,then
    ! Right justified
    ar27=ar26
  *else
    ! Left justified (default)
    ar27=0
  *endif

  ! Calculate X and Y coordinates of text annotation
  ar32=ANNOTXTARRAY_ARR_X(ar22)+ar27
  ar33=ANNOTXTARRAY_ARR_Y(ar22)

  ! Create text annotation
  anno,ar32,ar33,ar23

*enddo

! Clean up
*del , ANNOTXTARRAY_ARR_X         ,  , nopr
*del , ANNOTXTARRAY_ARR_Y         ,  , nopr
*del , ANNOTXTARRAY_VAR_charWidth ,  , nopr
