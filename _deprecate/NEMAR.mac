! VCELU, L,     bf,    dg,    d0,    sw,    [s0],  se,    [theta],  bos,   x0,    y0,    z0,    swi,   swf
! VCELU, arg1,  arg2,  arg3,  arg4,  arg5,  arg6,  arg7,  arg8,     arg9,  ar10,  ar11,  ar12,  ar13,  ar14

! Descrição:
! Criar solid model de uma viga de seção transversal tipo perfil I duplamente simétrico
! com aberturas circulares na alma (viga celular). As aberturas podem ou não ter
! enrijecedores e as larguras dos montantes inicial e final podem ser definidas diretamente.

! Lista de variáveis locais:
! AR20: Stiffener
! AR22: LMod
! AR23: CustomP0
! AR24: q1
! AR25: AQuant
! AR26: Rem
! AR27: P0
! AR28: COMPK_NEW
! AR29: COMPL_NEW
! AR30: COMPA_NEW
! AR31: MaxKP
! AR32: MaxLN
! AR33: CircArea
! AR34: CircAreas
! AR36: WebArea
! AR73: OutsideXMin
! AR74: OutsideXMax
! AR77: OutsideZMin
! AR78: OutsideZMax
! AR79: c
! AR80: COMPL_OpBorders
! AR81: MaxKP2
! AR82: DragLine1
! AR83: DragLine2
! AR84: ArgType
! AR85: LCount
! AR86: COMPL_DragLines
! AR90: LNum

! Lista de variáveis globais (apagadas ao final da subrotina):
! VCELU_ARR_LNUMS
! VCELU_ARR_KPNUMS

! Lista de variáveis de saída:
!    COMPONENTES:
!       VCELU_COMPA_ALMA
!          Componente contendo todas as áreas da alma da peça
!       VCELU_COMPA_MESAS
!          Componente contendo todas as áreas das mesas da peça
!       VCELU_COMPA_ENRIJECEDORES
!          Componente contendo todas as áreas dos enrijecedores
!          das aberturas (se houver algum)
!    GEOMETRIA:
!       VCELU_VAR_L
!       VCELU_VAR_bf
!       VCELU_VAR_dg
!       VCELU_VAR_d0
!       VCELU_VAR_sw
!       VCELU_VAR_s0
!       VCELU_VAR_se
!       VCELU_VAR_theta
!       VCELU_VAR_bos
!       VCELU_VAR_x0
!       VCELU_VAR_y0
!       VCELU_VAR_z0
!       VCELU_VAR_n
!       VCELU_VAR_swi
!       VCELU_VAR_swf

/prep7


!!                                                                                                                                            !!
!! 1.   DADOS CALCULADOS                                                                                                                      !!
!!                                                                                                                                            !!

! Verificar tipo de definição de geometria da abertura
*afun,DEG
*if,arg4,GT,0,THEN
    *if,arg5,GT,0,THEN
        arg7=arg4+arg5
    *elseif,arg7,GT,0,THEN
        arg5=arg7-arg4
    *else
        *msg,ERROR,0
        VCELU ERROR: INVALID PARAMETERS (%I).
    *endif
*else
    *msg,ERROR,1
    VCELU ERROR: INVALID PARAMETERS (%I).
*endif
arg8=60
arg6=arg4
*afun,RAD

*if,arg9,GT,0,THEN
    AR20=1
*else
    AR20=0
*endif
! Calcular comprimento modificado por largura de montante inicial
*if,ar13,GT,0,THEN
    *if,ar13,LT,arg1,THEN
        AR22=arg1-ar13
        AR23=1
    *else
        *msg,ERROR,2
        VCELU ERROR: INVALID W0 (%I).
    *endif
*else
    AR22=arg1
    AR23=0
*endif
! Calcular comprimento modificado por largura de montante final
*if,ar14,GT,0,THEN
    *if,ar14,LT,arg1,THEN
        AR22=AR22-ar14
        AR23=AR23+2
    *else
        *msg,ERROR,3
        VCELU ERROR: INVALID WF (%I).
    *endif
*endif
! Corrigir o comprimento de distribuição modificado
*if,AR23,EQ,1,THEN
    AR22=AR22+(arg5/2)
*elseif,AR23,EQ,2,THEN
    AR22=AR22+(arg5/2)
*elseif,AR23,EQ,3,THEN
    AR22=AR22+arg5
*endif
! Calcular quantidade de aberturas a ser criadas
AR24=AR22/arg7
AR25=NINT(AR24)
*if,AR25,GT,AR24,THEN
    AR25=AR25-1
*endif
!   Posição (em x) do eixo da primeira abertura
!     (mais próxima à face 1)
*if,AR23,EQ,0,THEN
    AR26=AR24-AR25
    AR27=((AR26*arg7)/2)+(arg5+arg6)/2
*elseif,AR23,EQ,1,THEN
    AR27=ar13+(arg6/2)
*elseif,AR23,EQ,2,THEN
    AR27=arg1-ar14-(AR25*arg7)+arg5+(arg6/2)
*elseif,AR23,EQ,3,THEN
    AR27=ar13+(arg6/2)
    arg1=AR27+AR25*arg7-(arg6/2)-arg5+ar14
    *msg,WARN,arg1
    COMPRIMENTO TOTAL ALTERADO PARA %10.4F.
*endif


!!                                                                                                                                            !!
!! 2.   SOLID MODEL                                                                                                                           !!
!!                                                                                                                                            !!

SELTOL,1e-8

! Desativar exibição de mensagens de aviso
/uis,MSGPOP,3

! Fazer backup das entidades do solid model atualmente selecionadas
KBACKUP
LBACKUP
ABACKUP

! Remover keypoints, linhas e áreas existentes da seleção
KSEL,U,KP,,ALL
LSEL,U,LINE,,ALL
ASEL,U,AREA,,ALL

! Criar componentes de keypoints, linhas e áreas novas
CM,AR28,KP
CM,AR29,LINE
CM,AR30,AREA

*get,AR31,KP,0,NUM,MAXD

*del,VCELU_ARR_KPNUMS,,nopr
*dim,VCELU_ARR_KPNUMS,ARRAY,18
*vfill,VCELU_ARR_KPNUMS,RAMP,AR31+1,1
NUMSTR,KP,AR31+1

! Keypoints da seção transversal (face 1)
K,VCELU_ARR_KPNUMS(1), ar10, ar11,      ar12-arg2/2
K,VCELU_ARR_KPNUMS(2), ar10, ar11,      ar12
K,VCELU_ARR_KPNUMS(3), ar10, ar11,      ar12+arg2/2
K,VCELU_ARR_KPNUMS(4), ar10, ar11+arg3, ar12-arg2/2
K,VCELU_ARR_KPNUMS(5), ar10, ar11+arg3, ar12
K,VCELU_ARR_KPNUMS(6), ar10, ar11+arg3, ar12+arg2/2

! Keypoints da seção transversal (face 2)
K,VCELU_ARR_KPNUMS(7),  ar10+arg1, ar11,      ar12-arg2/2
K,VCELU_ARR_KPNUMS(8),  ar10+arg1, ar11,      ar12
K,VCELU_ARR_KPNUMS(9),  ar10+arg1, ar11,      ar12+arg2/2
K,VCELU_ARR_KPNUMS(10), ar10+arg1, ar11+arg3, ar12-arg2/2
K,VCELU_ARR_KPNUMS(11), ar10+arg1, ar11+arg3, ar12
K,VCELU_ARR_KPNUMS(12), ar10+arg1, ar11+arg3, ar12+arg2/2

CM,AR28,KP

*get,AR32,LINE,0,NUM,MAXD

*del,VCELU_ARR_LNUMS,,nopr
*dim,VCELU_ARR_LNUMS,ARRAY,22
*vfill,VCELU_ARR_LNUMS,RAMP,AR32+1,1
NUMSTR,LINE,AR32+1

! Linhas da seção transversal (face 1)
L, VCELU_ARR_KPNUMS(1), VCELU_ARR_KPNUMS(2)
L, VCELU_ARR_KPNUMS(2), VCELU_ARR_KPNUMS(3)
L, VCELU_ARR_KPNUMS(4), VCELU_ARR_KPNUMS(5)
L, VCELU_ARR_KPNUMS(5), VCELU_ARR_KPNUMS(6)
L, VCELU_ARR_KPNUMS(2), VCELU_ARR_KPNUMS(5)

! Linhas da seção transversal (face 2)
L, VCELU_ARR_KPNUMS(7),  VCELU_ARR_KPNUMS(8)
L, VCELU_ARR_KPNUMS(8),  VCELU_ARR_KPNUMS(9)
L, VCELU_ARR_KPNUMS(10), VCELU_ARR_KPNUMS(11)
L, VCELU_ARR_KPNUMS(11), VCELU_ARR_KPNUMS(12)
L, VCELU_ARR_KPNUMS(8),  VCELU_ARR_KPNUMS(11)

! Linhas longitudinais
L, VCELU_ARR_KPNUMS(3), VCELU_ARR_KPNUMS(9)
L, VCELU_ARR_KPNUMS(2), VCELU_ARR_KPNUMS(8)
L, VCELU_ARR_KPNUMS(1), VCELU_ARR_KPNUMS(7)
L, VCELU_ARR_KPNUMS(6), VCELU_ARR_KPNUMS(12)
L, VCELU_ARR_KPNUMS(5), VCELU_ARR_KPNUMS(11)
L, VCELU_ARR_KPNUMS(4), VCELU_ARR_KPNUMS(10)

CM,AR29,LINE

! Áreas das mesas e alma do perfil
AL, VCELU_ARR_LNUMS(2),  VCELU_ARR_LNUMS(11), VCELU_ARR_LNUMS(7),  VCELU_ARR_LNUMS(12)
AL, VCELU_ARR_LNUMS(1),  VCELU_ARR_LNUMS(12), VCELU_ARR_LNUMS(6),  VCELU_ARR_LNUMS(13)
AL, VCELU_ARR_LNUMS(4),  VCELU_ARR_LNUMS(14), VCELU_ARR_LNUMS(9),  VCELU_ARR_LNUMS(15)
AL, VCELU_ARR_LNUMS(3),  VCELU_ARR_LNUMS(15), VCELU_ARR_LNUMS(8),  VCELU_ARR_LNUMS(16)
AL, VCELU_ARR_LNUMS(12), VCELU_ARR_LNUMS(10), VCELU_ARR_LNUMS(15), VCELU_ARR_LNUMS(5)

CM,AR30,AREA

! Caso haja ao menos uma abertura:
*if,AR25,GT,0,THEN

    ! Deslocar Working Plane
    WPOFFS,ar10,ar11,ar12

    ! Área da primeira abertura circular
    ASEL,U,AREA,,ALL
    CYL4, AR27, arg3/2, arg4/2
    *get,AR33,AREA,0,NUM,MIN

    ! Retornar Working Plane à posição original
    WPOFFS,-ar10,-ar11,-ar12
    WPSTYL,,,,,,,,0

    CM,AR28,KP
    CM,AR29,LINE

    ! Vértices do retângulo externo de corte
    AR73=ar10-arg1
    AR74=ar10+2*arg1
    AR77=ar12-arg2
    AR78=ar12+arg2

    ! Copiar área da abertura circular (AQUANT vezes)
    AGEN,AR25,AR33,,,arg7
    CM,AR34,AREA
    CMSEL,A,AR30
    CM,AR30,AREA

    ! Obter o número da área que representa a alma do perfil
    CMSEL,U,AR34
    ASEL,R,LOC,Z,ar12
    *get,AR36,AREA,0,NUM,MIN
    CMSEL,A,AR30
    CM,AR30,AREA
    CMSEL,A,AR34

    ! Cortar aberturas circulares
    ASBA,AR36,AR34,,,DELETE,DELETE
    CMSEL,A,AR30
    CM,AR30,AREA
    CMSEL,A,AR29
    CM,AR29,LINE
    CMSEL,A,AR28
    CM,AR28,KP

    ! Fatiar horizontalmente áreas no meio da altura
    CMSEL,S,AR28
    CMSEL,A,AR29
    CMSEL,A,AR30
    AR79=(arg3-arg4)/2
    SLICEXZ,ar11+arg3/2,AR73,AR77,AR74,AR78
    CM,AR28,KP
    CM,AR29,LINE
    CM,AR30,AREA

    ! Criar enrijecedores nas aberturas
    *if,AR20,EQ,1,THEN

        ! Selecionar apenas linhas das aberturas
        LSEL,S,LOC,Z,ar12
        LSEL,U,LOC,Y,ar11+arg3/2
        LSEL,U,LOC,Y,ar11
        LSEL,U,LOC,Y,ar11+arg3
        LSEL,U,LOC,X,ar10
        LSEL,U,LOC,X,arg10+arg1
        CM,AR80,LINE
        *get,AR85,LINE,,COUNT

        ! Criar linhas de direção de arrasto
        *get,AR81,KP,,NUM,MAXD
        K,AR81+1,ar10-arg1,ar11,ar12
        K,AR81+2,ar10-arg1,ar11,ar12-arg9
        K,AR81+3,ar10-arg1,ar11,ar12+arg9
        LSEL,U,LINE,,ALL
        L,AR81+1,AR81+2
        *get,AR82,LINE,,NUM,MAX
        L,AR81+1,AR81+3
        *get,AR83,LINE,,NUM,MAX
        CM,AR86,LINE

        ! Criar áreas dos enrijecedores de abertura
        AR90=0
        *do,AR86,1,AR85,1
            CMSEL,S,AR80
            AR90=LSNEXT(AR90)
            CMSEL,A,AR86
            ADRAG,AR90,,,,,,AR82
            ADRAG,AR90,,,,,,AR83
        *enddo
        LSLA,A
        KSLL,A

        ! Excluir entidades de arrasto
        LDELE,AR82
        LDELE,AR83
        KDELE,AR81+1
        KDELE,AR81+2
        KDELE,AR81+3

        ! Mesclar entidades
        NUMMRG,KP

        ! Atualizar componentes
        CMSEL,A,AR28
        CM,AR28,KP
        CMSEL,A,AR29
        CM,AR29,LINE
        CMSEL,A,AR30
        CM,AR30,AREA

    *endif

*endif

!!                                                                                                !!
!! 3.   SAÍDA                                                                                     !!
!!                                                                                                !!

! Definir componente de saída das mesas
ASEL,S,LOC,Y,ar11
ASEL,A,LOC,Y,ar11+arg3
CM,VCELU_COMPA_MESAS,AREA

! Definir componente de saída da alma
ASEL,INVE
ASEL,R,LOC,Z,ar12
CM,VCELU_COMPA_ALMA,AREA

! Definir componente de saída dos enrijecedores
*if,AR20,EQ,1,THEN
    CMSEL,S,AR30
    ASEL,U,LOC,Y,ar11+arg3
    ASEL,U,LOC,Y,ar11
    ASEL,U,LOC,Z,ar12
    CM,VCELU_COMPA_ENRIJECEDORES,AREA
*endif

! Restaurar seleção prévia
KRESTORE
LRESTORE
ARESTORE

! Selecionar novas entidades criadas
CMSEL,A,VCELU_COMPA_ALMA
CMSEL,A,VCELU_COMPA_MESAS
CMSEL,A,AR28
CMSEL,A,AR29
CMSEL,A,AR30

! Excluir componentes
CMDELE,AR28
CMDELE,AR29
CMDELE,AR30

*if,AR20,EQ,1,THEN
    CMSEL,A,VCELU_COMPA_ENRIJECEDORES
*endif

*del,VCELU_ARR_LNUMS,,nopr
*del,VCELU_ARR_KPNUMS,,nopr

*get,AR84,PARM,arg1,TYPE
*if,AR84,EQ,-1,THEN
    VCELU_VAR_L=0
*else
    VCELU_VAR_L=arg1
*endif
*get,AR84,PARM,arg2,TYPE
*if,AR84,EQ,-1,THEN
    VCELU_VAR_bf=0
*else
    VCELU_VAR_bf=arg2
*endif
*get,AR84,PARM,arg3,TYPE
*if,AR84,EQ,-1,THEN
    VCELU_VAR_dg=0
*else
    VCELU_VAR_dg=arg3
*endif
*get,AR84,PARM,arg4,TYPE
*if,AR84,EQ,-1,THEN
    VCELU_VAR_d0=0
*else
    VCELU_VAR_d0=arg4
*endif
*get,AR84,PARM,arg5,TYPE
*if,AR84,EQ,-1,THEN
    VCELU_VAR_sw=0
*else
    VCELU_VAR_sw=arg5
*endif
*get,AR84,PARM,arg6,TYPE
*if,AR84,EQ,-1,THEN
    VCELU_VAR_s0=0
*else
    VCELU_VAR_s0=arg6
*endif
*get,AR84,PARM,arg7,TYPE
*if,AR84,EQ,-1,THEN
    VCELU_VAR_se=0
*else
    VCELU_VAR_se=arg7
*endif
*get,AR84,PARM,arg8,TYPE
*if,AR84,EQ,-1,THEN
    VCELU_VAR_theta=0
*else
    VCELU_VAR_theta=arg8
*endif
*get,AR84,PARM,arg9,TYPE
*if,AR84,EQ,-1,THEN
    VCELU_VAR_bos=0
*else
    VCELU_VAR_bos=arg9
*endif
*get,AR84,PARM,ar10,TYPE
*if,AR84,EQ,-1,THEN
    VCELU_VAR_x0=0
*else
    VCELU_VAR_x0=ar10
*endif
*get,AR84,PARM,ar11,TYPE
*if,AR84,EQ,-1,THEN
    VCELU_VAR_y0=0
*else
    VCELU_VAR_y0=ar11
*endif
*get,AR84,PARM,ar12,TYPE
*if,AR84,EQ,-1,THEN
    VCELU_VAR_z0=0
*else
    VCELU_VAR_z0=ar12
*endif
*get,AR84,PARM,AR25,TYPE
*if,AR84,EQ,-1,THEN
    VCELU_VAR_n=0
*else
    VCELU_VAR_n=AR25
*endif
VCELU_VAR_swi=AR27-arg6/2
VCELU_VAR_swf=arg1-(AR25*arg7-arg5+VCELU_VAR_swi)
