! cmpatoe , newCMname , existingCMname
! CMPATOE , ARG1      , ARG2

! Description:
! Given an area selection component creates a corresponding element selection component, by applying
! ESLA,S.

! Arguments:
! ARG1: elemCompName  -  name of the newly created element selection component (optional).
!                        If this value is not provided the name defaults to COMPE_ARG2*,
!                        in which ARG2* is the substring of ARG2 after 'COMPA_'.
! ARG2: areaCompName  -  name of the existing area selection component.

! Local parameters:
! ar20: parType
! ar21: compeName
! ar22: compaPrefix
! ar23: compaSuffix


! Set the element selection component's name
*get,ar20,parm,ARG1,type
*if,ar20,ne,3,then
  ar22 = upcase(strsub(ARG2,1,6))
  *if,ar22,eq,'COMPA_',then
    ar23 = strsub(ARG2,7,32)
    ar21 = strcat('COMPE_',ar23)
  *else
    ar23 = strsub(ARG2,1,26)
    ar21 = strcat('COMPE_',ar23)
  *endif
*else
  ar21 = ARG1
*endif

! Prepare environment
ebackup
abackup

! Create new component
cmsel,s,%ARG2%
esla,s
cm,%ar21%,elem

! Clean up
elrestore
arestore
