! VIEW2W , layout , keepView , insideWindow , keepExisting , windowA , windowB
! VIEW2W , arg1   , arg2     , arg3         , arg4         , arg5    , arg6

! Description:
! Activates display in two windows. Window 1 occupies the left half of the screen and window 2
! occupies the right half of the screen.

! Arguments:
! arg1: layout        -  defines how the two windows are arranged:
!                           [0] left-right split (default)
!                           [1] top-bottom split
! arg2: keepView      -  key to determine whether or not to change view settings for the two windows
!                           [0] set isometric view for both windows (default)
!                           [1] do not change view settings for either window
! arg3: insideWindow  -  number of the window inside of which this split will be created
!                        (defaults to 0, using the entire screen space)
! arg4: keepExisting  -  key to determine whether or not to delete all current windows (default) or
!                        keep the remaining existing window layout:
!                           [0] delete all windows (default)
!                           [1] keep current layout
! arg5: windowA       -  number of top/left window (must be between 1 and 5, and defaults to 1)
! arg6: windowB       -  number of bottom/right window (must be between 1 and 5, and defaults to 2)

! Local parameters:
! AR20: parType
! AR21: xMin
! AR22: xMax
! AR23: yMin
! AR24: yMax
! AR25: xMid
! AR26: yMid
! AR31: windowAnumber
! AR32: windowBnumber

! Global parameters (erased at the end of this subroutine):
! -

! Set numbers for windows A and B
AR31=1
*get,AR20,parm,arg5,type
*if,AR20,eq,0,then
  *if,arg5,gt,0,then
    AR31=((arg5>1)<5)
  *endif
*endif

AR32=2
*get,AR20,parm,arg6,type
*if,AR20,eq,0,then
  *if,arg6,gt,0,then
    AR32=((arg6>1)<5)
  *endif
*endif

! Calculate bounding box for the created layout
*if,arg3,ge,1,and,arg3,le,5,then
  *get,AR21,graph,arg3,range,xmin
  *get,AR22,graph,arg3,range,xmax
  *get,AR23,graph,arg3,range,ymin
  *get,AR24,graph,arg3,range,ymax
*else
  AR21 = -1.00
  AR22 =  1.67
  AR23 = -1.00
  AR24 =  1.00
*endif

AR25=(AR22+AR21)/2
AR26=(AR24+AR23)/2

! Create window layout
*if,arg4,ne,1,then
  /window,all,dele
*endif

*if,arg1,eq,1,then
  ! Top-bottom split:
  /window , AR31 , AR21 , AR22 , AR26 , AR24
  /window , AR32 , AR21 , AR22 , AR23 , AR26

*else
  ! Left-right split:
  /window , AR31 , AR21 , AR25 , AR23 , AR24
  /window , AR32 , AR25 , AR22 , AR23 , AR24

*endif

! Set view
*if,arg2,ne,1,then
  /view , 1 , 1 ,  1 , 1
  /view , 2 , 1 , -1 , 1
  /angle , all , 0
  /auto  , all
*endif
