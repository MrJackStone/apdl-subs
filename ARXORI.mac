! ARXORI, xDir
! ARXORI, arg1

! Descrição:
! Esta subrotina serve para orientar o vetor normal de todas as áreas selecionadas com base
! no sentido imposto para a componente X do vetor normal.

! Lista de argumentos:
! arg1: xDir  -  sentido da componente X da normal (valor positivo ou negativo)

! Lista de variáveis locais:
! AR20: ACount
! AR21: nthArea
! AR22: L1
! AR23: L2
! AR24: L3
! AR25: K11
! AR26: K21
! AR27: K31
! AR28: K12
! AR29: K22
! AR30: K32
! AR31: K1
! AR32: K2
! AR33: K3
! AR34: XNorm
! AR35: revCheck
! AR36: ANum

! Obter número de áreas selecionadas
*get,AR20,AREA,,COUNT
AR36=0

! Orientar cada área selecionada
*do,AR21,1,AR20,1

   ! Obter número da área
   AR36=ARNEXT(AR36)

   ! Obter número de três das linhas do perímetro
   *get,AR22,AREA,AR36,LOOP,1,LINE,1
   *get,AR23,AREA,AR36,LOOP,1,LINE,2
   *get,AR24,AREA,AR36,LOOP,1,LINE,3

   ! Obter números dos keypoints das linhas
   *get,AR25,LINE,AR22,KP,1
   *get,AR26,LINE,AR23,KP,1
   *get,AR27,LINE,AR24,KP,1
   *get,AR28,LINE,AR22,KP,2
   *get,AR29,LINE,AR23,KP,2
   *get,AR30,LINE,AR24,KP,2

   ! Escolher três keypoints distintos
   AR31=AR25
   *if,AR31,EQ,AR26,THEN
      AR32=AR29
   *else
      AR32=AR26
   *endif
   *if,AR31,EQ,AR27,OR,AR32,EQ,AR27,THEN
      AR33=AR30
   *else
      AR33=AR27
   *endif

   ! Calcular vetor normal (componente na direção X)
   AR34=NORMKX(AR31,AR32,AR33)
   AR35=AR34*arg1

   ! Inverter a área (se necessário)
   *if,AR35,LT,0,THEN
      AREVERSE,AR36
   *endif

*enddo


