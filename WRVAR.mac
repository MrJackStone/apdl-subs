! WRVAR , xVar , yVar , fName , append ,      , folder
! WRVAR , arg1 , arg2 , arg3  , arg4   , arg5 , arg6

! Description:
! Transfers the contents of two time-history variables (one associated with the X axis and the other
! with the Y axis) to array parameters and writes their contents to an external file (.TXT)

! Arguments:
! arg1: xVar    -  number of time-history variable corresponding to X values
! arg2: yVar    -  number of time-history variable corresponding to Y values
! arg3: fName   -  name of the output file to which data will be written
! arg4: append  -  append key
!                     [0] Create new file
!                     [1] Append existing file
! arg5:         -  unused
! arg6: folder  -  name of the folder in which the file will be saved (optional).
!                  If a folder name is given, the folder will be created inside the current
!                  working directory. Defaults to the current working directory.

! Local parameters:
! AR20: parmType
! AR21: newFileKey
! AR22: appendFileKey
! AR24: outputFileName
! AR25: folderPrefix

! Global parameters (erased at the end of this subroutine):
! -

! Output parameters:
!    WRVAR_ARR_xValues
!       Array parameter with X values
!    WRVAR_ARR_yValues
!       Array parameter with Y values


! Define output file's name, prefixed with the optional folder name
AR25=''
*get,AR20,parm,arg6,type
*if,AR20,eq,3,then
  /syp,mkdir,arg6
  AR25=strcat(arg6,'\')
*endif
AR24=strcat(AR25,arg3)

! Set overwite/append option
AR22=''
*if,arg4,eq,1,then
  AR22='APPEND'
*endif

! Transfer X values
*del,WRVAR_ARR_xValues,,nopr
vget,WRVAR_ARR_xValues,arg1

! Transfer Y values
*del,WRVAR_ARR_yValues,,nopr
vget,WRVAR_ARR_yValues,arg2

! Write data do file
*cfopen,arg3,'TXT',,%AR22%
  *vwrite,WRVAR_ARR_xValues(1),WRVAR_ARR_yValues(1)
  (E20.8,' ',E20.8)
*cfclos
