! arruniq, inputArray, outputArray, sort
! ARRUNIQ,       ARG1,        ARG2, ARG3

! Description:
! Removes repeated elements from a sorted array.

! Arguments:
! ARG1: inputArray   -  name of array parameter to be operated on
! ARG2: outputArray  -  name of array parameter to which resulting list will be written
! ARG3: sort         -  array sorting key:
!                          [0] do not sort (default)
!                          [1] sort ascending
!                          [2] sort descending

! Implicit arguments
! ARRUNIQ_ARR_input  -  name of array parameter to be operated on
!                       (alternative to passing ARG1 as STRING)

! Local parameters:
! ar20: inputLength
! ar21: outputLength
! ar22: lastEntry
! ar23: nthEntry
! ar24: newEntry
! ar25: parmType
! ar26: inputParmName
! ar27: outputParmName

! Global parameters (erased at the end of this subroutine):
! ARRUNIQ_ARR_tempArray

! Output parameters:
! ARRUNIQ_ARR_OUTPUT
! ARRUNIQ_ARR_sorted


! Set input array parameter name
ar26 = 'ARRUNIQ_ARR_input'
*get,ar25,parm,ARG1,type
*if,ar25,eq,3,then
  ar26 = ARG1
*endif

! Set output array parameter name
ar27 = 'ARRUNIQ_ARR_OUTPUT'
*get,ar25,parm,ARG2,type
*if,ar25,eq,3,then
  ar27 = ARG2
*endif

! Set sorted input parameter name
ar28 = 'ARRUNIQ_ARR_sorted'
*del,%ar28%,,nopr

! Sort input array
*get,ar25,parm,ARG3,type
*if,ar25,eq,0,then
  *if,ARG3,eq,1,then
    *vfun,%ar28%,asort,%ar26%
  *elseif,ARG3,eq,2,then
    *vfun,%ar28%,dsort,%ar26%
  *else
    *vfun,%ar28%,copy,%ar26%
  *endif
*else
  *vfun,%ar28%,copy,%ar26%
*endif

! Get input array size
*get,ar20,parm,%ar28%,dim,x

! Initialize output array
ar21 = 1
*del,%ar27%,,nopr
*dim,%ar27%,array,ar21
ar22      = %ar28%(1)
%ar27%(1) = ar22

! Loop through input array values, starting from the second one:
*do,ar23,2,ar20,1

  ! Get value in next position
  ar24 = %ar28%(ar23)

  ! If value is different from previous one:
  *if,ar24,ne,ar22,then

    ! Increment output array length
    ar21 = ar21+1

    ! Fill temporary array with output values
    *del  , ARRUNIQ_ARR_tempArray ,       , nopr
    *dim  , ARRUNIQ_ARR_tempArray , array , ar21
    *vfun , ARRUNIQ_ARR_tempArray , copy  , %ar27%

    ! Append new value to output array
    *del,%ar27%,,nopr
    *dim,%ar27%,array,ar21
    *vfun,%ar27%,copy,ARRUNIQ_ARR_tempArray
    %ar27%(ar21) = ar24

    ! Update previous element value
    ar22=ar24

  *endif

*enddo

! Clear parameters
*del,ARRUNIQ_ARR_tempArray,,nopr
