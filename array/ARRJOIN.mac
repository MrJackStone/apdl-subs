! arrjoin , outputParm , arr1 , arr2 , arr3 , arr4 , arr5 , arr6 , arr7 , arr8 , arr9 , arr10 , arr11 , arr12 , arr13 , arr14 , arr15 , arr16 , arr17
! ARRJOIN , ARG1       , ARG2 , ARG3 , ARG4 , ARG5 , ARG6 , ARG7 , ARG8 , ARG9 , AR10 , AR11  , AR12  , AR13  , AR14  , AR15  , AR16  , AR17  , AR18

! Description:
! Joins multiple arrays (parameter names given) into a single one.

! Arguments:
! ARG1: outputParm  -  name of the output array parameter
! ARG2-AR19: arrXX  -  name of the input array parameters

! Local parameters:
! ar20: parType
! ar21: nthArg
! ar22: nthArrDim
! ar23: arrDim
! ar24: maskValue
! ar25: index

! Global erased parameters:
! ARRJOIN_ARR_mask
! ARRJOIN_VAR_parmName


*del,ARRJOIN_ARR_mask,,nopr
*dim,ARRJOIN_ARR_mask,,17
*vfill,ARRJOIN_ARR_mask,ramp,0,0

! 'ARG' arguments
ar23 = 0
*do,ar21,2,9,1
  *get,ar20,parm,arg%ar21%,type
  *if,ar20,eq,3,then

    ARRJOIN_VAR_parmName = arg%ar21%

    *cfopen,ARRJOIN_tmp,tmp
      *vwrite,ARRJOIN_VAR_parmName
*get,ar20,parm,%c,type
      *vwrite,
      ('*if,ar20,eq,1,then')
      *vwrite,ARRJOIN_VAR_parmName
*get,ar22,parm,%c,dim,x
      *vwrite,
      ('ar23=ar23+ar22')
      *vwrite,
      ('ARRJOIN_ARR_mask(ar21)=ar22')
      *vwrite,
      ('*elseif,ar20,eq,0,then')
      *vwrite,
      ('ar23=ar23+1')
      *vwrite,
      ('ARRJOIN_ARR_mask(ar21)=-1')
      *vwrite,
      ('*endif')
    *cfclos
    /nopr
    /input,ARRJOIN_tmp,tmp
    /delete,ARRJOIN_tmp,tmp
    /gopr

  *endif
*enddo

! 'AR' arguments
*do,ar21,10,17,1
  *get,ar20,parm,ar%ar21%,type
  *if,ar20,eq,3,then

    ARRJOIN_VAR_parmName = arg%ar21%

    *cfopen,ARRJOIN_tmp,tmp
      *vwrite,ARRJOIN_VAR_parmName
*get,ar20,parm,%c,type
      *vwrite,
      ('*if,ar20,eq,1,then')
      *vwrite,ARRJOIN_VAR_parmName
*get,ar22,parm,%c,dim,x
      *vwrite,
      ('ar23=ar23+ar22')
      *vwrite,
      ('ARRJOIN_ARR_mask(ar21)=ar22')
      *vwrite,
      ('*elseif,ar20,eq,0,then')
      *vwrite,
      ('ar23=ar23+1')
      *vwrite,
      ('ARRJOIN_ARR_mask(ar21)=-1')
      *vwrite,
      ('*endif')
    *cfclos
    /nopr
    /input,ARRJOIN_tmp,tmp
    /delete,ARRJOIN_tmp,tmp
    /gopr

  *endif
*enddo

*del,ARRJOIN_ARR_temp,,nopr
*dim,ARRJOIN_ARR_temp,,ar23

ar25 = 1
*do,ar21,2,17,1
  ar24 = ARRJOIN_ARR_mask(ar21)
  *if,ar24,ge,1,then
    ! Append array
    *if,ar21,le,9,then
      *vfun,ARRJOIN_ARR_temp(ar25),copy,arg%ar21%
    *else
      *vfun,ARRJOIN_ARR_temp(ar25),copy,ar%ar21%
    *endif
    ar25 = ar25+ar24

  *elseif,ar24,eq,-1,then
    ! Append scalar
    *if,ar21,le,9,then
      ARRJOIN_VAR_parmName = arg%ar21%
    *else
      ARRJOIN_VAR_parmName = ar%ar21%
    *endif

    *cfopen,ARRJOIN_tmp,tmp
      *vwrite,'ARRJOIN_ARR_temp',ar25,ARRJOIN_VAR_parmName
%c(%i)=%c
    *cfclos
    /nopr
    /input,ARRJOIN_tmp,tmp
    /delete,ARRJOIN_tmp,tmp
    /gopr
    ar25 = ar25+1

  *endif
*enddo

! Transfer data to output array
*del,%ARG1%,,nopr
*dim,%ARG1%,,ar23
*vfun,%ARG1%,copy,ARRJOIN_ARR_temp

! Clean up
*del,ARRJOIN_ARR_mask,,nopr
*del,ARRJOIN_ARR_temp,,nopr
